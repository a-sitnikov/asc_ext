
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Контрагент = Параметры.Контрагент;
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		СохраненнаяНастройка = ХранилищеОбщихНастроек.Загрузить("ЗагрузкаExcel_КВБанков", Строка(Контрагент.УникальныйИдентификатор()));
		Если ЗначениеЗаполнено(СохраненнаяНастройка) Тогда
			ЗагрузитьНастройкиСервер();
		КонецЕсли;	
	КонецЕсли;	
	
	ТабДок.МасштабПечати = 80;
	
	Если Параметры.Свойство("Таблица") Тогда
		
		Для каждого СтрокаТЗ из Параметры.Таблица Цикл
			
			НоваяСтрока = ТаблицаДокумента.Добавить();
			НоваяСтрока.Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТЗ.Документ, "Контрагент");
			НоваяСтрока.Наименование = ВРег(НоваяСтрока.Контрагент);
			
		КонецЦикла;	
		
	КонецЕсли;	
	
	НоваяСтрока = ТаблицаКолонок.Добавить();
	НоваяСтрока.Имя       = "Контрагент";
	НоваяСтрока.Заголовок = "Покупатель";
	НоваяСтрока.Тип       = "СправочникСсылка.Контрагенты";
	НоваяСтрока.Ключ      = Истина;
	
	НоваяСтрока = ТаблицаКолонок.Добавить();
	НоваяСтрока.Имя       = "Сумма";
	НоваяСтрока.Заголовок = "Сумма кредита";
	НоваяСтрока.Тип       = "Число";
	
	НоваяСтрока = ТаблицаКолонок.Добавить();
	НоваяСтрока.Имя       = "ПроцентКВ";
	НоваяСтрока.Заголовок = "Процент КВ";
	НоваяСтрока.Тип       = "Число";
	
	НоваяСтрока = ТаблицаКолонок.Добавить();
	НоваяСтрока.Имя       = "СуммаКВ";
	НоваяСтрока.Заголовок = "Сумма КВ";
	НоваяСтрока.Тип       = "Число";
	
	//Реквизиты
	ТипСтрока = Новый ОписаниеТипов("Строка");
	ДобавляемыеРеквизиты = Новый Массив;
	Для каждого СтрокаТЗ из ТаблицаКолонок Цикл
		
		Если СтрокаТЗ.Тип = "Число" Тогда
			Тип = Новый ОписаниеТипов("Число");
		Иначе	
			Тип = ТипСтрока;
		КонецЕсли;	
		
		Реквизит = Новый РеквизитФормы(СтрокаТЗ.Имя, Тип, "Таблица");
		ДобавляемыеРеквизиты.Добавить(Реквизит);
		
		Если СтрокаТЗ.Ключ Тогда
			Реквизит = Новый РеквизитФормы(СтрокаТЗ.Имя + "Док", Новый ОписаниеТипов(СтрокаТЗ.Тип), "Таблица");
			ДобавляемыеРеквизиты.Добавить(Реквизит);
		КонецЕсли;	
		
	КонецЦикла;
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
		
	Для каждого СтрокаТЗ из ТаблицаКолонок Цикл
		
		//Таблица
		Элемент = Элементы.Вставить("Таблица" + СтрокаТЗ.Имя, Тип("ПолеФормы"), Элементы.Таблица,);
		Элемент.Вид         = ВидПоляФормы.ПолеВвода;
		Элемент.ПутьКДанным = "Таблица." + СтрокаТЗ.Имя;
		Элемент.Заголовок   = СтрокаТЗ.Заголовок;
		
		Если СтрокаТЗ.Ключ  Тогда
			Элемент = Элементы.Вставить("Таблица" + СтрокаТЗ.Имя + "Док", Тип("ПолеФормы"), Элементы.Таблица,);
			Элемент.Вид         = ВидПоляФормы.ПолеВвода;
			Элемент.ПутьКДанным = "Таблица." + СтрокаТЗ.Имя + "Док";
			Элемент.Заголовок   = СтрокаТЗ.Заголовок + " (док)";
		КонецЕсли;	
		
		//Настройки
		Группа = Элементы.Вставить("Группа" + СтрокаТЗ.Имя, Тип("ГруппаФормы"), Элементы.ГруппаНомераКолонок);
		Группа.Вид          = ВидГруппыФормы.ОбычнаяГруппа;
		Группа.Заголовок    = "Группа мест";
		Группа.ОтображатьЗаголовок = Ложь;
		Группа.Группировка  = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		
		Элемент = Элементы.Вставить("Колонка" + СтрокаТЗ.Имя, Тип("ПолеФормы"), Группа,);
		Элемент.Вид         = ВидПоляФормы.ПолеВвода;
		Элемент.ПутьКДанным = "Колонка" + СтрокаТЗ.Имя;
		Элемент.Заголовок   = СтрокаТЗ.Заголовок;
		
		Кнопка = Элементы.Вставить("УстановитьНомерКолонки" + СтрокаТЗ.Имя, Тип("КнопкаФормы"), Группа,);
		Кнопка.ИмяКоманды = "УстановитьНомерКолонки";
		
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ВыбранаСохраненнаяНастройка" Тогда
		
		Если Параметр.УИДФормы = УникальныйИдентификатор Тогда
			
			СохраненнаяНастройка = Параметр.СохраненнаяНастройка;
			СохранитьНастройки();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
КонецПроцедуры

#КонецОбласти

#Область Настройки

&НаСервере
Процедура СохранитьНастройки()
	
	Если СохраненнаяНастройка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаОбъект = СохраненнаяНастройка.ПолучитьОбъект();
	НастройкаОбъект.ХранилищеНастроек   = Новый ХранилищеЗначения(ПолучитьСтруктуруНастроек());
	НастройкаОбъект.Записать();
	
	Модифицированность = Ложь;
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		ХранилищеОбщихНастроек.Сохранить("ЗагрузкаExcel_КВБанков", Строка(Контрагент.УникальныйИдентификатор()), СохраненнаяНастройка);
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСтруктуруНастроек(ВозвращатьАдрес = Ложь)
	
	СтруктураНастроек = Новый Структура;
	
	Для каждого СтрокаТЗ из ТаблицаКолонок Цикл
		ИмяКолонки = "Колонка" + СтрокаТЗ.Имя;
		СтруктураНастроек.Вставить(ИмяКолонки,  ЭтаФорма[ИмяКолонки]);
	КонецЦикла;	
		
	Если ВозвращатьАдрес Тогда
		Возврат ПоместитьВоВременноеХранилище(СтруктураНастроек);
	Иначе
		Возврат СтруктураНастроек;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура СохранитьНастройку(Команда)
	
	НастраиваемыйОбъект = "АСЦ_ОтчетПосредника.ЗагрузкаИзExcel";
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("СинонимОбъекта",           НастраиваемыйОбъект);
	СтруктураНастроек.Вставить("РежимСохраненияНастройки", Истина);
	СтруктураНастроек.Вставить("ЗакрыватьПриВыборе", Истина);
	СтруктураНастроек.Вставить("РежимВыбора",        Истина);
	СтруктураНастроек.Вставить("Отбор", Новый Структура("НастраиваемыйОбъект, ТипНастройки"
														, НастраиваемыйОбъект
														, ПредопределенноеЗначение("Перечисление.ТипыНастроек.НастройкиОбработки")));
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("РежимСохраненияНастройки", Истина);
	ДополнительныеПараметры.Вставить("УИДФормы", УникальныйИдентификатор);
    ОписаниеОЗакрытии = Новый ОписаниеОповещения("ВыбратьНастройкуФормы_Завершение", ЭтотОбъект, ДополнительныеПараметры);	
	ОткрытьФорму("Справочник.СохраненныеНастройки.Форма.ФормаВыбора_Управляемая", СтруктураНастроек, , , , , ОписаниеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьНастройкуФормы_Завершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	
	Если ВыбранноеЗначение <> Неопределено Тогда
		
		СохраненнаяНастройка = ВыбранноеЗначение;
		СохранитьНастройки();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПриИзменении(Элемент)
	
	ЗагрузитьнастройкиСервер();
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройкиСервер()
	
	Если СохраненнаяНастройка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	СпрОбъект = СохраненнаяНастройка.ПолучитьОбъект();
	СтруктураНастроек = СпрОбъект.ХранилищеНастроек.Получить();
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, СтруктураНастроек);
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		ХранилищеОбщихНастроек.Сохранить("ЗагрузкаExcel_КВБанков", Строка(Контрагент.УникальныйИдентификатор()), СохраненнаяНастройка);
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ИмяФайлаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗапуститьПриложение(ИмяФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИмяФайлаНачалоВыбораЗавершение", ЭтотОбъект);
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = "Файлы Excel (*.xls, *.xlsx)|*.xls; *.xlsx";
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ИмяФайла = ВыбранныеФайлы[0];
	
КонецПроцедуры

&НаКлиенте
Процедура Прочитать(Команда)
	
	Если ПустаяСтрока(ИмяФайла) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не указана файл";
		Сообщение.Поле  = "ИмяФайла";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(СохраненнаяНастройка) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не указана настройка";
		Сообщение.Поле  = "СохраненнаяНастройка";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;	
	
	Файл = Новый Файл(ИмяФайла);
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Действие", "Загрузка");
	ДопПараметры.Вставить("Расширение", Файл.Расширение);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПрочитатьЗавершение", ЭтотОбъект, ДопПараметры);
	НачатьПомещениеФайлов(ОписаниеОповещения, , ИмяФайла , Ложь, Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьЗавершение(ПомещенныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ПомещенныеФайлы = Неопределено Тогда
        Возврат;
    КонецЕсли;
	
    ПрочитатьНаСервере(ПомещенныеФайлы[0].Хранение, ДополнительныеПараметры);
	
	Элементы.ТаблицаПеренестиВДокумент.КнопкаПоУмолчанию = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНаСервере(АдресХранилища, ДополнительныеПараметры)
	
    ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресХранилища);
    
    ИмяВременногоФайла = ПолучитьИмяВременногоФайла(ДополнительныеПараметры.Расширение);
    ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.Прочитать(ИмяВременногоФайла);
	
	Если ДополнительныеПараметры.Действие = "Просмотр" Тогда
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройки;
		
	Иначе
		
		ПрочитатьДанные();
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанные()
	
	Таблица.Очистить();
	
	Для Счетчик = 1 по ТабДок.ВысотаТаблицы Цикл
		
		ОбластьСумма = ТабДок.Область(Счетчик, КолонкаСумма, Счетчик);
		Сумма = АСЦ_ОбщийМодуль.вЧисло(ОбластьСумма.Текст);
		Если Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;	
		
		ОбластьКонтрагент = ТабДок.Область(Счетчик, КолонкаКонтрагент);
		Если СтрДлина(ОбластьКонтрагент.Текст) <= 3 Тогда
			Продолжить;
		КонецЕсли;	
		
		НоваяСтрока = Таблица.Добавить();
		НоваяСтрока.Сумма = Сумма;
		
		НоваяСтрока.Контрагент = СокрЛП(ОбластьКонтрагент.Текст);
		
		Если КолонкаДоговор <> 0 Тогда
			ОбластьДоговор = ТабДок.Область(Счетчик, КолонкаДоговор);
			НоваяСтрока.Договор = ОбластьДоговор.Текст;
		КонецЕсли;	
		
		Если КолонкаПроцентКВ <> 0 Тогда
			ОбластьПроцентКВ = ТабДок.Область(Счетчик, КолонкаПроцентКВ);
			ПроцентКВТекст = ОбластьПроцентКВ.Текст;
			ПроцентКВТекст = СтрЗаменить(ПроцентКВТекст, "%", "");
			НоваяСтрока.ПроцентКВ = АСЦ_ОбщийМодуль.вЧисло(ПроцентКВТекст);
		КонецЕсли;	
		
		Если КолонкаСуммаКВ <> 0 Тогда
			ОбластьСуммаКВ = ТабДок.Область(Счетчик, КолонкаСуммаКВ);
			НоваяСтрока.СуммаКВ = АСЦ_ОбщийМодуль.вЧисло(ОбластьСуммаКВ.Текст);
		КонецЕсли;	
		
	КонецЦикла;	
	
	Элементы.ТаблицаКонтрагент.ТекстПодвала = "Всего записей: " + Таблица.Количество();
	Элементы.ТаблицаСумма.ТекстПодвала   = Таблица.Итог("Сумма");
	Элементы.ТаблицаСуммаКВ.ТекстПодвала = Таблица.Итог("СуммаКВ");
	
	КоличествоНеНайденоДок = 0;
	Для каждого СтрокаТЗ из Таблица Цикл
		
		НайденныеСтроки = ТаблицаДокумента.НайтиСтроки(Новый Структура("Наименование", ВРег(СтрокаТЗ.Контрагент)));
		Если НайденныеСтроки.Количество() > 0 Тогда
			СтрокаТЗ.КонтрагентДок = НайденныеСтроки[0].Контрагент;
			НайденныеСтроки[0].Найден = Истина;
		Иначе	
			КоличествоНеНайденоДок = КоличествоНеНайденоДок + 1; 	
		КонецЕсли;	
		
	КонецЦикла;	
	
	НайденныеСтроки = ТаблицаДокумента.НайтиСтроки(Новый Структура("Найден", Ложь));
	ТекстОшибкиФайл = "В файле не найдено соотвествие для " + НайденныеСтроки.Количество() + " покупателей из документа";
	Элементы.ТекстОшибкиФайл.Видимость = (НайденныеСтроки.Количество() > 0);
	
	ТекстОшибкиДок = "В документе не найдено соотвествие для " + КоличествоНеНайденоДок + " покупателей из файла";
	Элементы.ТекстОшибкиДок.Видимость = (КоличествоНеНайденоДок > 0);
	
КонецПроцедуры	

&НаКлиенте
Процедура Просмотр(Команда)
	
	Если ПустаяСтрока(ИмяФайла) Тогда
		Возврат;
	КонецЕсли;	
	
	Файл = Новый Файл(ИмяФайла);
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Действие",   "Просмотр");
	ДопПараметры.Вставить("Расширение", Файл.Расширение);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПрочитатьЗавершение", ЭтотОбъект, ДопПараметры);
	НачатьПомещениеФайлов(ОписаниеОповещения, , ИмяФайла , Ложь, Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНомерКолонки(Команда)
	
	Область = Элементы.ТабДок.ТекущаяОбласть;
	
	Имя = СтрЗаменить(ТекущийЭлемент.Имя, "УстановитьНомерКолонки", "");
	ЭтаФорма["Колонка" + Имя] = Область.Лево;
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСоотвествиеКолонки(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьСоотвествиеКолонкиЗавершение", ЭтотОбъект);
	
	СписокКолонок = Новый СписокЗначений;
	Для каждого СтрокаТЗ из  ТаблицаКолонок Цикл
		СписокКолонок.Добавить(СтрокаТЗ.Имя, СтрокаТЗ.Заголовок);
	КонецЦикла;	
	СписокКолонок.ПоказатьВыборЭлемента(ОписаниеОповещения, "Выберите колонку");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСоотвествиеКолонкиЗавершение(ВыбранныйЭлемент, Доппараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Область = Элементы.ТабДок.ТекущаяОбласть;
	
	ЭтаФорма["Колонка" + ВыбранныйЭлемент.Значение] = Область.Лево;
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	Закрыть(Таблица);
	
КонецПроцедуры


&НаКлиенте
Процедура ТолькоНеНайденные(Команда)
	
	Элементы.ТаблицаДокументаТолькоНеНайденные.Пометка = НЕ Элементы.ТаблицаДокументаТолькоНеНайденные.Пометка;
	
КонецПроцедуры

