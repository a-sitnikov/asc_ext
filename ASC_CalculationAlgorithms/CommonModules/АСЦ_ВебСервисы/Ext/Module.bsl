
Функция ПолучитьДанныеДокументаРТУ(ДокументСсылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Ссылка",            ДокументСсылка);
	Запрос.Параметры.Вставить("СвойствоСтраховая", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "Страховая"));
	Запрос.Параметры.Вставить("СвойствоIDUnicus",  ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ID_Unicus"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Док.Дата КАК Дата,
	|	Док.Номер КАК Номер,
	|	Док.РучнаяКорректировка КАК РучнаяКорректировка,
	|	Док.Организация КАК Организация,
	|	Док.Организация.ИНН КАК ОрганизацияИНН,
	|	Док.Организация.КПП КАК ОрганизацияКПП,
	|	Док.Контрагент КАК Контрагент,
	|	Док.Контрагент.Наименование КАК КонтрагентНаименование,
	|	Док.Контрагент.ИНН КАК КонтрагентИНН,
	|	Док.Контрагент.КПП КАК КонтрагентКПП,
	|	Док.Контрагент.ДокументУдостоверяющийЛичность КАК КонтрагентПаспорт,
	|	Док.ДоговорКонтрагента КАК Договор,
	|	Док.ДоговорКонтрагента.Наименование КАК ДоговорНаименование,
	|	Док.ДоговорКонтрагента.Дата КАК ДоговорДата,
	|	Док.ДоговорКонтрагента.ДатаНачала КАК ДоговорДатаНачала,
	|	Док.ДоговорКонтрагента.СрокДействия КАК ДоговорСрокДействия,
	|	Док.ДоговорКонтрагента.ОсновнойЦФО.Наименование КАК ЦФО,
	|	Док.СуммаДокумента КАК Сумма,
	|	ЕСТЬNULL(ДокАгентскиеУслуги.Номенклатура, ДокУслуги.Номенклатура) КАК Номенклатура,
	|	СпрНоменклатура.Наименование КАК НоменклатураНаименование,
	|	СпрНоменклатура.НоменклатурнаяГруппа.Наименование КАК НоменклатураНГ,
	|	ДоговорыСтраховая.Значение КАК Страховая,
	|	ЕСТЬNULL(Страховые.ИНН, """") КАК СтраховаяИНН,
	|	ЕСТЬNULL(Страховые.КПП, """") КАК СтраховаяКПП,
	|	ЕСТЬNULL(Страховые.Наименование, """") КАК СтраховаяНаименование,
	|	ЕСТЬNULL(ДокАгентскиеУслуги.Сумма, 0) КАК СуммаАгенскиеУслуги,
	|	ЕСТЬNULL(ДокУслуги.Сумма, 0) КАК СуммаУслуги,
	|	СвойстваIDUnicus.Значение КАК id_unicus
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК Док
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Услуги КАК ДокУслуги
	|		ПО (ДокУслуги.Ссылка = Док.Ссылка)
	|			И (ДокУслуги.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК ДокАгентскиеУслуги
	|		ПО (ДокАгентскиеУслуги.Ссылка = Док.Ссылка)
	|			И (ДокАгентскиеУслуги.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО (ЕСТЬNULL(ДокАгентскиеУслуги.Номенклатура, ДокУслуги.Номенклатура) = СпрНоменклатура.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов.ДополнительныеРеквизиты КАК ДоговорыСтраховая
	|		ПО Док.ДоговорКонтрагента = ДоговорыСтраховая.Ссылка
	|			И (ДоговорыСтраховая.Свойство = &СвойствоСтраховая)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Страховые
	|		ПО (ДоговорыСтраховая.Значение = Страховые.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК СвойстваIDUnicus
	|		ПО Док.Ссылка = СвойстваIDUnicus.Объект
	|			И (СвойстваIDUnicus.Свойство = &СвойствоIDUnicus)
	|ГДЕ
	|	Док.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	Иначе	
		Возврат Результат.Выгрузить()[0];
	КонецЕсли;	
	
КонецФункции	

Функция ВыгрузкаОФУ_Реализации(ДатаНач, ДатаКон, ОрганизацияИНН, ОрганизацияКПП, ТолькоБУ) Экспорт
	
	Если ТолькоБУ = Неопределено Тогда
		ТолькоБУ = Истина;
	КонецЕсли;	
	
	СписокДокументов = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.ascgroup.ru/OFU", "СписокРТУ"));
	
	Запрос = Новый ПостроительЗапроса;
	Запрос.Параметры.Вставить("Дата1", ДатаНач);
	Запрос.Параметры.Вставить("Дата2", ДатаКон);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК Док
	|ГДЕ
	|	Док.Проведен
	|	И Док.Дата МЕЖДУ &Дата1 И &Дата2
	|{ГДЕ
	|	Док.Организация,
	|	Док.РучнаяКорректировка}";
	
	Если ЗначениеЗаполнено(ОрганизацияИНН) Тогда
		
		Организация = АСЦ_ПоискОбъектов.ПолучитьОрганизациюПоИНН(ОрганизацияИНН, ОрганизацияКПП);
		
		ЭлементОтбора = Запрос.Отбор.Добавить("Организация");
		ЭлементОтбора.Установить(Организация, Истина);
		
	КонецЕсли;	
	
	Если ТолькоБУ = Истина Тогда
		
		ЭлементОтбора = Запрос.Отбор.Добавить("РучнаяКорректировка");
		ЭлементОтбора.Установить(Ложь, Истина);
		
	КонецЕсли;	
	
	Запрос.Выполнить();
	Выборка = Запрос.Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Данные = ПолучитьДанныеДокументаРТУ(Выборка.Ссылка);
		
		Документ = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.ascgroup.ru/OFU", "РТУ"));
		Документ.ГУИД  = Строка(Выборка.Ссылка.УникальныйИдентификатор());
		Документ.Дата  = Данные.Дата;
		Документ.Номер = Данные.Номер;
		Документ.Тип   = "Реализация";
		Документ.БУ    = НЕ Данные.РучнаяКорректировка;
		
		// Организация
		пОрганизация = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.ascgroup.ru/OFU", "Организация"));
		пОрганизация.ГУИД = Строка(Данные.Организация.УникальныйИдентификатор());
		пОрганизация.ИНН  = Данные.ОрганизацияИНН;
		пОрганизация.КПП  = Данные.ОрганизацияКПП;
		
		Документ.Организация = пОрганизация;
		
		// Основание
		пОснование = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.ascgroup.ru/OFU", "Основание"));
		пОснование.ГУИД = ""; 
		пОснование.Тип  = "";
		
		Документ.Основание = пОснование;
		
		// Контрагент
		пКонтрагент = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.ascgroup.ru/OFU", "Контрагент"));
		пКонтрагент.ГУИД         = Строка(Данные.Контрагент.УникальныйИдентификатор());
		пКонтрагент.Наименование = Данные.КонтрагентНаименование;
		пКонтрагент.ИНН          = Данные.КонтрагентИНН;
		пКонтрагент.КПП          = Данные.КонтрагентКПП;
		пКонтрагент.Паспорт      = Данные.КонтрагентПаспорт;

		Документ.Контрагент = пКонтрагент;
		
		// Договор
		пДоговор = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.ascgroup.ru/OFU", "Договор"));
		пДоговор.ГУИД         = Строка(Данные.Договор.УникальныйИдентификатор());
		пДоговор.Наименование = Данные.ДоговорНаименование;
		пДоговор.Дата         = Данные.ДоговорДата;
		пДоговор.ДатаНачала   = Данные.ДоговорДатаНачала;
		пДоговор.СрокДействия = Данные.ДоговорСрокДействия;
		
		Документ.Договор = пДоговор;
		
		// Страховая
		пСтраховая = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.ascgroup.ru/OFU", "Контрагент"));
		пСтраховая.ГУИД         = ?(ЗначениеЗаполнено(Данные.Страховая), Строка(Данные.Страховая.УникальныйИдентификатор()), "");
		пСтраховая.Наименование = Данные.СтраховаяНаименование;
		пСтраховая.ИНН          = Данные.СтраховаяИНН;
		пСтраховая.КПП          = Данные.СтраховаяКПП;

		Документ.Страховая = пСтраховая;
		
		// Номенклатура
		пНоменклатура = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.ascgroup.ru/OFU", "Номенклатура"));
		пНоменклатура.Наименование         = Данные.НоменклатураНаименование;
		пНоменклатура.НоменклатурнаяГруппа = Данные.НоменклатураНГ;
		
		Документ.Номенклатура = пНоменклатура;
		
		Документ.ДЦ        = Данные.ЦФО;
		Документ.Сумма     = Данные.СуммаУслуги + Данные.СуммаАгенскиеУслуги;
		Документ.СуммаКВ   = Данные.СуммаУслуги;
		Документ.id_unicus = Данные.id_unicus;
		
		СписокДокументов.Документы.Добавить(Документ);
		
	КонецЦикла;	
	
	Возврат СписокДокументов;
	
КонецФункции

Функция ВыгрузкаОФУ_ОтчетПосредника(ДатаНач, ДатаКон, ОрганизацияИНН, ОрганизацияКПП, ТолькоБУ) Экспорт
	
	Если ТолькоБУ = Неопределено Тогда
		ТолькоБУ = Истина;
	КонецЕсли;	
	
	СписокДокументов = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.ascgroup.ru/OFU", "СписокОтчетовПосредника"));
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый ПостроительЗапроса;
	Запрос.Параметры.Вставить("Дата1", ДатаНач);
	Запрос.Параметры.Вставить("Дата2", ДатаКон);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.Номер КАК Номер,
	|	Док.Организация КАК Организация,
	|	Док.Организация.ИНН КАК ОрганизацияИНН,
	|	Док.Организация.КПП КАК ОрганизацияКПП,
	|	Док.Контрагент КАК Страховая,
	|	Док.Контрагент.ИНН КАК СтраховаяИНН,
	|	Док.Контрагент.КПП КАК СтраховаяКПП,
	|	Док.Контрагент.Наименование КАК СтраховаяНаименование,
	|	Док.ДоговорКонтрагента КАК Договор,
	|	Док.ДоговорКонтрагента.Наименование КАК ДоговорНаименование,
	|	Док.Продажи.(
	|		Документ КАК Документ,
	|		Документ.РучнаяКорректировка КАК ДокументРучнаяКорректировка,
	|		Контрагент КАК Контрагент,
	|		Контрагент.Наименование КАК КонтрагентНаименование,
	|		Контрагент.ИНН КАК КонтрагентИНН,
	|		Контрагент.КПП КАК КонтрагентКПП,
	|		Контрагент.ДокументУдостоверяющийЛичность КАК КонтрагентПаспорт,
	|		ДоговорКонтрагента КАК Договор,
	|		ДоговорКонтрагента.Наименование КАК ДоговорНаименование,
	|		ДоговорКонтрагента.Дата КАК ДоговорДата,
	|		ДоговорКонтрагента.ДатаНачала КАК ДоговорДатаНачала,
	|		ДоговорКонтрагента.СрокДействия КАК ДоговорСрокДействия,
	|		СуммаОплаты КАК Сумма,
	|		СуммаКВ КАК СуммаКВ,
	|		id КАК id_unicus
	|	) КАК Продажи
	|ИЗ
	|	Документ.АСЦ_ОтчетПосредника КАК Док
	|ГДЕ
	|	Док.Проведен
	|	И Док.Дата МЕЖДУ &Дата1 И &Дата2
	|{ГДЕ
	|	Док.Организация}";
	
	Если ЗначениеЗаполнено(ОрганизацияИНН) Тогда
		
		Организация = АСЦ_ПоискОбъектов.ПолучитьОрганизациюПоИНН(ОрганизацияИНН, ОрганизацияКПП);
		
		ЭлементОтбора = Запрос.Отбор.Добавить("Организация");
		ЭлементОтбора.Установить(Организация, Истина);
		
	КонецЕсли;	
	
	Запрос.Выполнить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Выборка = Запрос.Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Документ = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.ascgroup.ru/OFU", "ОтчетПосредника"));
		Документ.ГУИД  = Строка(Выборка.Ссылка.УникальныйИдентификатор());
		Документ.Дата  = Выборка.Дата;
		Документ.Номер = Выборка.Номер;
		
		// Организация
		пОрганизация = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.ascgroup.ru/OFU", "Организация"));
		пОрганизация.ГУИД = Строка(Выборка.Организация.УникальныйИдентификатор());
		пОрганизация.ИНН  = Выборка.ОрганизацияИНН;
		пОрганизация.КПП  = Выборка.ОрганизацияКПП;
		
		Документ.Организация = пОрганизация;
		
		// Страховая
		пСтраховая = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.ascgroup.ru/OFU", "Контрагент"));
		пСтраховая.ГУИД         = Строка(Выборка.Страховая.УникальныйИдентификатор());
		пСтраховая.Наименование = Выборка.СтраховаяНаименование;
		пСтраховая.ИНН          = Выборка.СтраховаяИНН;
		пСтраховая.КПП          = Выборка.СтраховаяКПП;
		
		Документ.Страховая = пСтраховая;
		
		// Договор
		пДоговор = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.ascgroup.ru/OFU", "Договор"));
		пДоговор.ГУИД         = Строка(Выборка.Договор.УникальныйИдентификатор());
		пДоговор.Наименование = Выборка.ДоговорНаименование;
		
		Документ.Договор = пДоговор;
		
		// ТЧ
		ТипСтрокиТЧ = Документ.Тип().Свойства.Получить("ТЧ").Тип;
		
		ВыборкаТЧ = Выборка.Продажи.Выбрать();
		Пока ВыборкаТЧ.Следующий() Цикл
			
			Если ТолькоБУ = Истина
				И ВыборкаТЧ.ДокументРучнаяКорректировка Тогда
				
				Продолжить;
				
			КонецЕсли;	
			
			пСтрокаТЧ = ФабрикаXDTO.Создать(ТипСтрокиТЧ);
		
			// Контрагент
			пКонтрагент = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.ascgroup.ru/OFU", "Контрагент"));
			пКонтрагент.ГУИД         = Строка(ВыборкаТЧ.Контрагент.УникальныйИдентификатор());
			пКонтрагент.Наименование = ВыборкаТЧ.КонтрагентНаименование;
			пКонтрагент.ИНН          = ВыборкаТЧ.КонтрагентИНН;
			пКонтрагент.КПП          = ВыборкаТЧ.КонтрагентКПП;
			пКонтрагент.Паспорт      = ВыборкаТЧ.КонтрагентПаспорт;
			
			пСтрокаТЧ.Контрагент = пКонтрагент;
			
			// Договор
			пДоговор = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.ascgroup.ru/OFU", "Договор"));
			пДоговор.ГУИД         = Строка(ВыборкаТЧ.Договор.УникальныйИдентификатор());
			пДоговор.Наименование = ВыборкаТЧ.ДоговорНаименование;
			пДоговор.Дата         = ВыборкаТЧ.ДоговорДата;
			пДоговор.ДатаНачала   = ВыборкаТЧ.ДоговорДатаНачала;
			пДоговор.СрокДействия = ВыборкаТЧ.ДоговорСрокДействия;
			
			пСтрокаТЧ.Договор = пДоговор;
			
			// Документ
			пОснование = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.ascgroup.ru/OFU", "Основание"));
			пОснование.ГУИД = Строка(ВыборкаТЧ.Документ.УникальныйИдентификатор()); 
			пОснование.Тип  = "Реализация";
			
			пСтрокаТЧ.Документ = пОснование;
			
			// Общие
			пСтрокаТЧ.Сумма     = ВыборкаТЧ.Сумма;
			пСтрокаТЧ.Сумма     = ВыборкаТЧ.Сумма;
			пСтрокаТЧ.СуммаКВ   = ВыборкаТЧ.СуммаКВ;
			пСтрокаТЧ.id_unicus = ВыборкаТЧ.id_unicus;
			пСтрокаТЧ.БУ        = НЕ ВыборкаТЧ.ДокументРучнаяКорректировка;
			
			Документ.ТЧ.Добавить(пСтрокаТЧ);
			
		КонецЦикла;	
		
		СписокДокументов.Документы.Добавить(Документ);
		
	КонецЦикла;	
	
	Возврат СписокДокументов;
		
КонецФункции
