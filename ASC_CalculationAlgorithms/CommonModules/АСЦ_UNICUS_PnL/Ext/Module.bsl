
// Данная процедура вызываются из обработчика "Процедура заполнения" 
// правила обработки отчета
//
Процедура ЗаполнитьОтчет(ДокОбъект) Экспорт
	
	ДокОбъект.ОчиститьВсе();
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Дата1",        ДокОбъект.ПериодОтчета.ДатаНачала);
	Запрос.Параметры.Вставить("Дата2",        КонецДня(ДокОбъект.ПериодОтчета.ДатаОкончания));
	Запрос.Параметры.Вставить("Номенклатура", Справочники.Номенклатура.НайтиПоНаименованию("Страховки"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(Выборка.Количество) КАК Количество,
	|	СУММА(Выборка.СуммаВыручка) КАК СуммаВыручка,
	|	СУММА(Выборка.СуммаАгентские) КАК СуммаАгентские,
	|	Выборка.Номенклатура КАК Номенклатура,
	|	Выборка.СтатьяДоходов.Код КАК СтатьяДоходовКод
	|ИЗ
	|	(ВЫБРАТЬ
	|		1 КАК Количество,
	|		Док.Сумма КАК СуммаВыручка,
	|		0 КАК СуммаАгентские,
	|		Док.Номенклатура КАК Номенклатура,
	|		Док.Ссылка.ДоговорКонтрагента.ОсновнаяСтатьяИсполнение КАК СтатьяДоходов
	|	ИЗ
	|		Документ.РеализацияТоваровУслуг.Услуги КАК Док
	|	ГДЕ
	|		Док.Ссылка.Проведен
	|		И Док.Ссылка.Дата МЕЖДУ &Дата1 И &Дата2
	|		И Док.Номенклатура В ИЕРАРХИИ(&Номенклатура)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		0,
	|		0,
	|		Док.Сумма,
	|		Док.Номенклатура,
	|		Док.Ссылка.ДоговорКонтрагента.ОсновнаяСтатьяИсполнение
	|	ИЗ
	|		Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК Док
	|	ГДЕ
	|		Док.Ссылка.Проведен
	|		И Док.Ссылка.Дата МЕЖДУ &Дата1 И &Дата2
	|		И Док.Номенклатура В ИЕРАРХИИ(&Номенклатура)) КАК Выборка
	|
	|СГРУППИРОВАТЬ ПО
	|	Выборка.Номенклатура,
	|	Выборка.СтатьяДоходов.Код";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Колонка = Выборка.СтатьяДоходовКод;
		
		ДокОбъект.УстановитьЗначениеПоказателя("Выручка" + Колонка + "_Колво", Выборка.Количество, Выборка.Номенклатура);
		ДокОбъект.УстановитьЗначениеПоказателя("Выручка" + Колонка + "_Сумма", Выборка.СуммаВыручка + Выборка.СуммаАгентские, Выборка.Номенклатура);
		ДокОбъект.УстановитьЗначениеПоказателя("Прибыль" + Колонка + "_Сумма", Выборка.СуммаВыручка, Выборка.Номенклатура);
		
	КонецЦикла;	
	
КонецПроцедуры
