
// Вычисляет значение З1*К1 + З2*К2 + ... + Зn*Кn в разрезе аналитик
// Аналитики показателей должны быть одинаковы
// Аналог функции СУММАПРОИЗВ() из Excel
//
// Параметры:
//	ДокументОбъект - ДокументОбъект.НастраиваемыйОтчет
//	КодПоказателяЗначение - Строка
//	КодПоказателяКоличество - Строка
//	ИтогоКоличество - Число, куда возвращается сумма по показателю КодПоказателяКоличество
//
// Возвращаемое значение: Число
//
Функция СуммаПроизведение(ДокументОбъект, КодПоказателяЗначение, КодПоказателяКоличество, ИтогоКоличество = 0) Экспорт
	
	ТаблицаЗначение = ДокументОбъект.ТаблицаТекущихЗначенийПоказателей(КодПоказателяЗначение);
	ТаблицаКоличество = ДокументОбъект.ТаблицаТекущихЗначенийПоказателей(КодПоказателяКоличество);
	
	СтруктураОтбора = Новый Структура("Аналитика1, Аналитика2, Аналитика3");
	
	ИтогоСумма      = 0;
	ИтогоКоличество = 0;
	Для каждого СтрокаЗначение из ТаблицаЗначение Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаЗначение);

		НайденныеСтроки = ТаблицаКоличество.НайтиСтроки(СтруктураОтбора);
		Если НайденныеСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;	
		
		СтрокаКоличество = НайденныеСтроки[0];
 		ИтогоСумма      = ИтогоСумма      + СтрокаЗначение.Значение * СтрокаКоличество.Значение;
		ИтогоКоличество = ИтогоКоличество + СтрокаКоличество.Значение;
		
	КонецЦикла;

	Возврат ИтогоСумма;
	
КонецФункции

// Вычисляет значение (З1*К1 + З2*К2 + ... + Зn*Кn)/(К1 + ... + Кn) в разрезе аналитик
// Аналитики показателей должны быть одинаковы
//
// Параметры:
//	ДокументОбъект - ДокументОбъект.НастраиваемыйОтчет
//	КодПоказателяЗначение - Строка
//	КодПоказателяКоличество - Строка
//
// Возвращаемое значение: Число
//
Функция Средневзвешанное(ДокументОбъект, КодПоказателяЗначение, КодПоказателяКоличество) Экспорт
	
	ИтогоКоличество = 0;
	ИтогоСумма = СуммаПроизведение(ДокументОбъект, КодПоказателяЗначение, КодПоказателяКоличество, ИтогоКоличество);
	
	ТаблицаЗначение = ДокументОбъект.ТаблицаТекущихЗначенийПоказателей(КодПоказателяЗначение);
	ТаблицаКоличество = ДокументОбъект.ТаблицаТекущихЗначенийПоказателей(КодПоказателяКоличество);
	
	Если ИтогоКоличество = 0 Тогда
		Возврат 0;
	Иначе	
		Возврат ИтогоСумма / ИтогоКоличество;
	КонецЕсли;	
	
КонецФункции	


// Возвращает ссылку на контрагента
// Порядок поиска
//  1. ИНН + КПП, если заполены ИНН и КПП
//  2. ИНН, если поиск по ИНН+КПП не выдал результата или если КПП не заполнено
//  Если при поиске по ИНН найдено более 1 контрагента, то используется параметр "ВозвращатьПервого"
//  Если по ИНН+КПП не нашли, но по ИНН нашли, то проверяется совпадает ли КПП (возможно ошибка в КПП)
//  3. Наименование
//
// Параметры:
//	Наименование - Строка
//	ИНН - Строка
//	КПП - Строка
//	ТекстОшибки - Строка, куда возвращается описание ошибки поиска
//	ВозвращатьПервого - Булево, если найдено более 1 контрагента, то возвращается первый, иначе пустая ссылка
//
// Возвращаемое значение: СправочникСсылка.Контрагенты
//
Функция НайтиКонтрагента(Наименование, ИНН, КПП, ТекстОшибки = "", ВозвращатьПервого = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Наименование", Наименование);
	Запрос.Параметры.Вставить("ИНН",          ИНН);
	Запрос.Параметры.Вставить("КПП",          КПП);
	
	Если ЗначениеЗаполнено(ИНН) Тогда
		
		Если ЗначениеЗаполнено(КПП) Тогда
			
			Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Спр.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Контрагенты КАК Спр
			|ГДЕ
			|	НЕ Спр.ЭтоГруппа
			|	И НЕ Спр.ПометкаУдаления
			|	И Спр.ИНН = &ИНН
			|	И Спр.КПП = &КПП";
			
			Результат = Запрос.Выполнить();
			Если НЕ Результат.Пустой() Тогда
				Возврат Результат.Выгрузить()[0][0];
			КонецЕсли;
		
		КонецЕсли;
		
		// Возможно в КПП Ошибка или не указан, поищем по ИНН
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 2
		|	Спр.Ссылка КАК Ссылка,
		|	Спр.КПП КАК КПП
		|ИЗ
		|	Справочник.Контрагенты КАК Спр
		|ГДЕ
		|	НЕ Спр.ЭтоГруппа
		|	И НЕ Спр.ПометкаУдаления
		|	И Спр.ИНН = &ИНН";
		
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			Возврат Справочники.Контрагенты.ПустаяСсылка();
		Иначе
			
			Таблица = Результат.Выгрузить();
			Если Таблица.Количество() = 2 Тогда
				// Более 1 контрагента с одинаковым ИНН
				Если ЗначениеЗаполнено(КПП) Тогда
					ТекстОшибки = Наименование + ". Не найден с КПП: " + КПП + ", но с ИНН: " + ИНН + " более 1";
				Иначе	
					ТекстОшибки = Наименование + ". Найдено более 1 контрагента с ИНН: " + ИНН;
				КонецЕсли;	
				
				Если ВозвращатьПервого Тогда
					Возврат Таблица[0].Ссылка;
				Иначе	
					Возврат Справочники.Контрагенты.ПустаяСсылка();
				КонецЕсли;
				
			Иначе
				
				СтрокаТЗ = Таблица[0]; 
				Если ЗначениеЗаполнено(КПП) Тогда
					ТекстОшибки = Наименование + ". Не совпадает КПП: " + КПП + ". В базе: " + СтрокаТЗ.КПП;
				КонецЕсли;	
				
				Возврат СтрокаТЗ.Ссылка;
				
			КонецЕсли;	
		
		КонецЕсли;
		
	Иначе
		
		// По наименованию ищем любой
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Спр.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Контрагенты КАК Спр
		|ГДЕ
		|	НЕ Спр.ЭтоГруппа
		|	И НЕ Спр.ПометкаУдаления
		|	И Спр.Наименование = &Наименование";
		
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			Возврат Результат.Выгрузить()[0][0];
		Иначе	
			Возврат Справочники.Контрагенты.ПустаяСсылка();
		КонецЕсли;
		
	КонецЕсли;	
	
КонецФункции

// Возвращает ссылку на контрагента.
// Если контрагент отсутствует, то создается новый
// Для поиска вызывается функция НайтиКонтрагента
//
// Параметры:
//	Данные - Структура
//		Контрагент - Строка, наименование контрагента
//		КонтрагентИНН - Строка	
//		КонтрагентКПП - Строка
//		КонтрагентПаспортСерия - Строка
//		КонтрагентПаспортНомер - Строка
//		КонтрагентПаспортДата - Строка
//	Параметры - Структура
//		КонтрагентРодительФЛ - СправочникСсылка.Контрагенты
//		КонтрагентРодительЮЛ - СправочникСсылка.Контрагенты
//	Перезаписывать - булево
//		Если контрагент уже существует, то будут обновлены его реквизиты
// Возвращаемое значение: СправочникСсылка.Контрагенты
//
Функция ПолучитьКонтрагента(Данные, Параметры, Перезаписывать = Ложь) Экспорт
	
	СпрСсылка  = Неопределено;
	СтрПаспорт = "";
	
	Если ЗначениеЗаполнено(Данные.КонтрагентИНН) Тогда
		СпрСсылка = АСЦ_ОбщийМодуль.НайтиКонтрагента(Данные.Контрагент, Данные.КонтрагентИНН, Данные.КонтрагентКПП);
		ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
		Родитель  = Параметры.КонтрагентРодительЮЛ;
	Иначе
		
		СтрПаспорт = СокрЛП(Данные.КонтрагентПаспортСерия) + " " + СокрЛП(Данные.КонтрагентПаспортНомер); 
		Если ЗначениеЗаполнено(Данные.КонтрагентПаспортДата) Тогда
			СтрПаспорт = СтрПаспорт + " выдан: "  + Формат(Данные.КонтрагентПаспортДата, "ДФ=dd.MM.yyyy");
		КонецЕсли;
		СтрПаспорт = СокрЛП(СтрПаспорт);
		
		ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
		Родитель   = Параметры.КонтрагентРодительФЛ;
		
		Запрос = Новый Запрос;
		
		Если ЗначениеЗаполнено(СтрПаспорт) Тогда
			
			Запрос.Параметры.Вставить("Паспорт", СтрПаспорт);
			Запрос.Текст =
			"ВЫБРАТЬ
			|	Спр.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Контрагенты КАК Спр
			|ГДЕ
			|	НЕ Спр.ПометкаУдаления
			|	И Спр.ДокументУдостоверяющийЛичность = &Паспорт";
			
			Результат = Запрос.Выполнить();
			Если НЕ Результат.Пустой() Тогда
				СпрСсылка = Результат.Выгрузить()[0][0];
			КонецЕсли;
			
		КонецЕсли;
		
		Если СпрСсылка = Неопределено Тогда
			
			Запрос.Параметры.Вставить("Наименование", Данные.Контрагент);
			Запрос.Параметры.Вставить("Родитель",     Родитель);
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	Спр.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Контрагенты КАК Спр
			|ГДЕ
			|	НЕ Спр.ПометкаУдаления
			|	И Спр.Наименование = &Наименование";
			
			Результат = Запрос.Выполнить();
			Если НЕ Результат.Пустой() Тогда
				СпрСсылка = Результат.Выгрузить()[0][0];
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СпрСсылка) Тогда
		
		Если Перезаписывать Тогда
			СпрОбъект = СпрСсылка.ПолучитьОбъект();
		Иначе
			Возврат СпрСсылка;
		КонецЕсли;	
		
	Иначе	
		
		СпрОбъект = Справочники.Контрагенты.СоздатьЭлемент();
		
	КонецЕсли;	
		
	СпрОбъект.Родитель     = Родитель;
	СпрОбъект.Наименование = Данные.Контрагент;
	СпрОбъект.ИНН          = Данные.КонтрагентИНН;
	СпрОбъект.КПП          = Данные.КонтрагентКПП;
	СпрОбъект.ДокументУдостоверяющийЛичность = СтрПаспорт;
	СпрОбъект.ЮридическоеФизическоеЛицо = ЮридическоеФизическоеЛицо;
	СпрОбъект.СтранаРегистрации         = Справочники.СтраныМира.Россия;
	СпрОбъект.Записать();
	
	Возврат СпрОбъект.Ссылка;
		
КонецФункции


// Возвращает ссылку на договор контрагента
//
// Параметры:
//	Контрагент    - СправочникСсылка.Контрагенты
//	Организация   - СправочникСсылка.Организации
//	Наименование  - Строка
//	ВидДоговораУХ - ПеречислениеСсылка.ВидыДоговоровКонтрагентовУХ
//
// Возвращаемое значение: СправочникСсылка.ДоговорыКонтрагентов
//
Функция НайтиДоговор(Контрагент, Организация, Наименование, ВидДоговораУХ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Наименование", Наименование);
	Запрос.Параметры.Вставить("Контрагент",   Контрагент);
	Запрос.Параметры.Вставить("Организация",  Организация);
	Запрос.Параметры.Вставить("ВидДоговораУХ", ВидДоговораУХ);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Спр.Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК Спр
	|ГДЕ
	|	Спр.Наименование = &Наименование
	|	И Спр.Владелец = &Контрагент
	|	И Спр.Организация = &Организация
	|	И Спр.ВидДоговораУХ = &ВидДоговораУХ
	|	И НЕ Спр.ПометкаУдаления";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Возврат Результат.Выгрузить()[0][0];
	Иначе
		Возврат Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	КонецЕсли;	
		
КонецФункции

// Возвращает ссылку на договор контрагента.
// Если договор контрагента отсутствует, то создается новый
// Для поиска вызывается функция НайтиДоговор или ОсновнойДоговор
//
// Параметры:
//	Данные - Структура
//		Контрагент    - СправочникСсылка.Контрагенты
//		Организация   - СправочникСсылка.Организации	
//		Наименование  - Строка
//		ВидДоговораУХ - ПеречислениеСсылка.ВидыДоговоровКонтрагентовУХ
//		Дата          - Дата
//		ДатаНачала    - Дата
//		СрокДействия  - Дата
//		ЦФО           - Справочник.Организации
//		СтатьяДДС      - Справочник.СтатьиДвиженияДенежныхСредств
//		СтатьяРасходов - Справочник.СтатьиДоходовИРасходов
//	Перезаписывать - булево
//		Если контрагент уже существует, то будут обновлены его реквизиты
//	Основной - булево
//		Поиск основного договора (из регистра основных договоров) или по наименованию
//
// Возвращаемое значение: СправочникСсылка.Контрагенты
//
Функция ПолучитьДоговор(Данные, Перезаписывать, Основной = Ложь) Экспорт
	
	СпрОбъект = Неопределено;
	
	Если Основной Тогда
		СпрСсылка = ОсновнойДоговор(Данные.Контрагент, Данные.Организация, Данные.ВидДоговораУХ);
	Иначе	
		СпрСсылка = НайтиДоговор(Данные.Контрагент, Данные.Организация, Данные.Наименование, Данные.ВидДоговораУХ);
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(СпрСсылка) Тогда
		
		Если Перезаписывать Тогда
			СпрОбъект = СпрСсылка.ПолучитьОбъект();
		Иначе	
			Возврат СпрСсылка;
		КонецЕсли;
		
	КонецЕсли;	
	
	Если СпрОбъект = Неопределено Тогда
		СпрОбъект = Справочники.ДоговорыКонтрагентов.СоздатьЭлемент();
	КонецЕсли;

	// Основные свойства
	СпрОбъект.Владелец      = Данные.Контрагент;
	СпрОбъект.Организация   = Данные.Организация;
	СпрОбъект.Наименование  = Данные.Наименование;
	СпрОбъект.ВидДоговораУХ = Данные.ВидДоговораУХ;
	
	// Вычисляемые свойства
	СпрОбъект.Номер         = СпрОбъект.Наименование;
	СпрОбъект.ВидДоговора   = УправлениеДоговорамиУХКлиентСерверПовтИсп.ВидДоговораБП(СпрОбъект.ВидДоговораУХ);
	СпрОбъект.ВидСоглашения = Перечисления.ВидыСоглашений.ДоговорСУсловием;
	СпрОбъект.ВалютаВзаиморасчетов = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	// Доп. свойства
	Данные.Свойство("Дата",           СпрОбъект.Дата);
	Данные.Свойство("ДатаНачала",     СпрОбъект.ДатаНачала);
	Данные.Свойство("СрокДействия",   СпрОбъект.СрокДействия);
	Данные.Свойство("ЦФО",            СпрОбъект.ОсновнойЦФО);
	Данные.Свойство("СтатьяДДС",      СпрОбъект.ОсновнаяСтатьяДвиженияДенежныхСредств);
	Данные.Свойство("СтатьяДоходов",  СпрОбъект.ОсновнаяСтатьяИсполнение);
	Данные.Свойство("АналитикаБДДС1", СпрОбъект.АналитикаБДДС1);
	Данные.Свойство("АналитикаБДДС2", СпрОбъект.АналитикаБДДС2);
	Данные.Свойство("АналитикаБДДС3", СпрОбъект.АналитикаБДДС3);
	Данные.Свойство("АналитикаИсполнение1", СпрОбъект.АналитикаИсполнение1);
	Данные.Свойство("АналитикаИсполнение2", СпрОбъект.АналитикаИсполнение2);
	Данные.Свойство("АналитикаИсполнение3", СпрОбъект.АналитикаИсполнение3);
	
	СпрОбъект.Записать();
	
	Если Основной Тогда
		
		Запись = РегистрыСведений.ОсновныеДоговорыКонтрагента.СоздатьМенеджерЗаписи();
		Запись.Организация = Данные.Организация;
		Запись.Контрагент  = Данные.Контрагент;
		Запись.ВидДоговора = СпрОбъект.ВидДоговора;
		Запись.Договор     = СпрОбъект.Ссылка;
		Запись.Записать();
		
	КонецЕсли;	
	
	Возврат СпрОбъект.Ссылка;
		
	
КонецФункции

// Возвращает ссылку на договор контрагента.
//
// Параметры:
//	Контрагент    - СправочникСсылка.Контрагенты
//	Организация   - СправочникСсылка.Организации	
//	ВидДоговораУХ - ПеречислениеСсылка.ВидыДоговоровКонтрагентовУХ
//
// Возвращаемое значение: СправочникСсылка.Контрагенты
//
Функция ОсновнойДоговор(Контрагент, Организация, ВидДоговораУХ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Контрагент",    Контрагент);
	Запрос.Параметры.Вставить("Организация",   Организация);
	Запрос.Параметры.Вставить("ВидДоговораУХ", ВидДоговораУХ);
	Запрос.Параметры.Вставить("ВидДоговора",   УправлениеДоговорамиУХКлиентСерверПовтИсп.ВидДоговораБП(ВидДоговораУХ));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Рег.Договор КАК Договор
	|ИЗ
	|	РегистрСведений.ОсновныеДоговорыКонтрагента КАК Рег
	|ГДЕ
	|	Рег.Организация = &Организация
	|	И Рег.Контрагент = &Контрагент
	|	И Рег.ВидДоговора = &ВидДоговора";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Возврат Результат.Выгрузить()[0][0];
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Спр.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК Спр
	|ГДЕ
	|	Спр.Владелец = &Контрагент
	|	И Спр.Организация = &Организация
	|	И Спр.ВидДоговораУХ = &ВидДоговораУХ
	|	И НЕ Спр.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка УБЫВ";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Возврат Результат.Выгрузить()[0][0];
	КонецЕсли;
	
КонецФункции	


Функция ПолучитьОбъектПоСоотвествию(Наименование, Настройка, База, Кэш) Экспорт
	
	СпрСсылка = Кэш[Наименование];
	Если СпрСсылка <> Неопределено Тогда
		Возврат СпрСсылка;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Наименование", Наименование);
	Запрос.Параметры.Вставить("База",         База);
	Запрос.Параметры.Вставить("Настройка",    Настройка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Рег.ОбъектТекущейИБ КАК ОбъектТекущейИБ
	|ИЗ
	|	РегистрСведений.СоответствиеОбъектовТекущейИВнешнихИБ КАК Рег
	|ГДЕ
	|	Рег.ОбъектВнешнейИБ = &Наименование
	|	И Рег.НастройкаСоответствия = &Настройка
	|	И Рег.ИспользуемаяИБ = &База";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		СпрСсылка = Справочники.Организации.ПустаяСсылка();
	Иначе
		СпрСсылка = Результат.Выгрузить()[0][0];
	КонецЕсли;	
	
	Кэш.Вставить(Наименование, СпрСсылка);
	Возврат СпрСсылка;
	
КонецФункции

Функция ПолучитьОрганизациюПоИНН(ИНН, Кэш) Экспорт
	
	СпрСсылка = Кэш[ИНН];
	Если СпрСсылка <> Неопределено Тогда
		Возврат СпрСсылка;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("ИНН", ИНН);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Спр.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Спр
	|ГДЕ
	|	Спр.ИНН = &ИНН";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		СпрСсылка = Результат.Выгрузить()[0][0];
	Иначе
		СпрСсылка = Справочники.Организации.ПустаяСсылка();
	КонецЕсли;	
	
	Кэш.Вставить(ИНН, СпрСсылка);
	Возврат СпрСсылка;
	
КонецФункции	

Функция НайтиБанк(БИК) Экспорт
	
	Если НЕ ЗначениеЗаполнено(БИК) Тогда
		Возврат Справочники.Банки.ПустаяСсылка();
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Код", БИК);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Спр.Ссылка
	|ИЗ
	|	Справочник.Банки КАК Спр
	|ГДЕ
	|	НЕ Спр.ПометкаУдаления
	|	И Спр.Код = &Код";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Возврат Результат.Выгрузить()[0][0];
	Иначе
		Возврат Справочники.Банки.ПустаяСсылка();
	КонецЕсли;	
	
КонецФункции

