#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// ПОДГОТОВКА ПАРАМЕТРОВ ПРОВЕДЕНИЯ ДОКУМЕНТА

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт
	
	ТаблицыДляДвижений = Новый Структура;		
	ДополнительныеСвойства = Новый Структура;
	НомераТаблиц = Новый Структура;
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",    ДокументСсылка);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаНачисленноеКВ(НомераТаблиц);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		Если СтрНачинаетсяС(НомерТаблицы.Ключ, "Таблица") Тогда			
			Продолжить;
		КонецЕсли;
		ТаблицыДляДвижений.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
			
	ДополнительныеСвойства.Вставить("ТаблицыДляДвижений", ТаблицыДляДвижений);
	
	Возврат ДополнительныеСвойства;

КонецФункции

Функция ТекстЗапросаНачисленноеКВ(НомераТаблиц)

	НомераТаблиц.Вставить("АСЦ_НачисленноеКВ", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Док.Ссылка.Дата КАК Период,
	|	Док.ДЦ КАК ДЦ,
	|	Док.Статья КАК Статья,
	|	Док.Ссылка.Контрагент КАК Комитент,
	|	Док.Ссылка.Организация КАК Организация,
	|	Док.Реализация КАК Реализация,
	|	Док.Документ КАК Документ,
	|	-ВЫБОР
	|		КОГДА Док.Ссылка.ВидОперации.ДвиженияСумма = ""Сумма""
	|			ТОГДА Док.СуммаПлан
	|		ИНАЧЕ Док.СуммаКВПлан
	|	КОНЕЦ КАК Сумма,
	|	-Док.СуммаКВПлан КАК СуммаКВ
	|ИЗ
	|	Документ.АСЦ_ОтчетПосредникаУпр.Продажи КАК Док
	|ГДЕ
	|	Док.Ссылка = &Ссылка
	|	И НЕ Док.Ссылка.ВидОперации.ФормироватьКорректировки
	|	И (Док.СуммаПлан > 0
	|			ИЛИ Док.СуммаКВПлан > 0)
	|	И (Док.Сумма <> Док.СуммаПлан
	|			ИЛИ Док.СуммаКВ <> Док.СуммаКВПлан)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Док.Ссылка.Дата,
	|	Док.ДЦ,
	|	Док.Статья,
	|	Док.Ссылка.Контрагент,
	|	Док.Ссылка.Организация,
	|	Док.Реализация,
	|	Док.Документ,
	|	ВЫБОР
	|		КОГДА Док.Ссылка.ВидОперации.ДвиженияСумма = ""Сумма""
	|			ТОГДА Док.Сумма
	|		ИНАЧЕ Док.СуммаКВ
	|	КОНЕЦ,
	|	Док.СуммаКВ
	|ИЗ
	|	Документ.АСЦ_ОтчетПосредникаУпр.Продажи КАК Док
	|ГДЕ
	|	Док.Ссылка = &Ссылка
	|	И НЕ Док.Ссылка.ВидОперации.ФормироватьКорректировки
	|	И (Док.Сумма > 0
	|			ИЛИ Док.СуммаКВ > 0)
	|	И (Док.Сумма <> Док.СуммаПлан
	|			ИЛИ Док.СуммаКВ <> Док.СуммаКВПлан)";
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ПолучитьТаблицуВсехПолей() Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Имя");
	Таблица.Колонки.Добавить("Реквизит");
	
	ДобавитьВТаблицу(Таблица, "Контрагент");
	ДобавитьВТаблицу(Таблица, "НоменклатурнаяГруппа");
	ДобавитьВТаблицу(Таблица, "ПродажиДокумент");
	ДобавитьВТаблицу(Таблица, "ПродажиКонтрагент");
	ДобавитьВТаблицу(Таблица, "ПродажиЦФО");
	ДобавитьВТаблицу(Таблица, "ПродажиРеализация");
	ДобавитьВТаблицу(Таблица, "ПродажиРеализацияОрганизация");
	ДобавитьВТаблицу(Таблица, "ПродажиVIN");
	ДобавитьВТаблицу(Таблица, "ПродажиСумма");
	ДобавитьВТаблицу(Таблица, "ПродажиПроцентКВ");
	ДобавитьВТаблицу(Таблица, "ПродажиСуммаПлан");
	ДобавитьВТаблицу(Таблица, "ПродажиПроцентКВПлан");
	ДобавитьВТаблицу(Таблица, "ГруппаПроцентКВ");
	
	Возврат Таблица;
	
КонецФункции	

Функция ПолучитьНастройкиПолей(ВидОперации) Экспорт
	
	Таблица = ПолучитьТаблицуВсехПолей();
	Таблица.Колонки.Добавить("Видимость", Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("Заголовок");
	
	Если ВидОперации = Справочники.АСЦ_ВидыОперацийПлановоеНачислениеКВ.Страховки
		ИЛИ ВидОперации = Справочники.АСЦ_ВидыОперацийПлановоеНачислениеКВ.ПрямыеРасчетыССК Тогда	
		
		УстановитьВидимостьВТаблице(Таблица, "Контрагент", "Страховая");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиДокумент");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиКонтрагент");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиVIN");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиСумма", "Сумма оплаты (факт)");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиСуммаПлан", "Сумма оплаты (план)");
		
	ИначеЕсли ВидОперации = Справочники.АСЦ_ВидыОперацийПлановоеНачислениеКВ.ДопКВСтраховки Тогда	
		
		УстановитьВидимостьВТаблице(Таблица, "Контрагент", "Страховая");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиДокумент");
		
	ИначеЕсли ВидОперации = Справочники.АСЦ_ВидыОперацийПлановоеНачислениеКВ.ОплатаВБанке Тогда	
		
		УстановитьВидимостьВТаблице(Таблица, "Контрагент", "Банк");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиДокумент");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиКонтрагент");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиСумма", "Сумма оплаты (факт)");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиПроцентКВ");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиСуммаПлан", "Сумма оплаты (план)");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиПроцентКВПлан");
		
	ИначеЕсли ВидОперации = Справочники.АСЦ_ВидыОперацийПлановоеНачислениеКВ.ДиагностическаяКарта Тогда	
		
		УстановитьВидимостьВТаблице(Таблица, "Контрагент", "Страховая");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиСумма", "Сумма оплаты (факт)");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиСуммаПлан", "Сумма оплаты (план)");
		
	ИначеЕсли ВидОперации = Справочники.АСЦ_ВидыОперацийПлановоеНачислениеКВ.КВБанков Тогда	
		
		УстановитьВидимостьВТаблице(Таблица, "НоменклатурнаяГруппа");
		УстановитьВидимостьВТаблице(Таблица, "Контрагент", "Банк");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиДокумент");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиКонтрагент");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиРеализация", "Реализация а/м");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиРеализацияОрганизация", "Продавец а/м");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиVIN");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиСумма", "Полная сумма кредита (факт)");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиПроцентКВ");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиСуммаПлан", "Полная сумма кредита (план)");
		УстановитьВидимостьВТаблице(Таблица, "ПродажиПроцентКВПлан");
		УстановитьВидимостьВТаблице(Таблица, "ГруппаПроцентКВ");
		
	КонецЕсли;	
		
	Возврат Таблица;
	
КонецФункции

Процедура ДобавитьВТаблицу(Таблица, Имя, Реквизит = "")
	
	НоваяСтрока = Таблица.Добавить();
	НоваяСтрока.Имя      = Имя;
	НоваяСтрока.Реквизит = Реквизит;
	
КонецПроцедуры	

Процедура УстановитьВидимостьВТаблице(Таблица, Имя, Заголовок = "")
	
	НоваяСтрока = Таблица.Найти(Имя, "Имя");
	НоваяСтрока.Видимость = Истина;
	НоваяСтрока.Заголовок = Заголовок;
	
КонецПроцедуры	

#КонецЕсли
