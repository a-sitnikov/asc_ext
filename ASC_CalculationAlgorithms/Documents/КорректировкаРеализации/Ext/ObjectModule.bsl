
&После("ОбработкаПроведения")
Процедура АСЦ_ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Добавим движения по КУДиР
	// считаем, что документ уже оплачен, ибо создаются по оплаченным полисам
	Движения.КнигаУчетаДоходовИРасходов.Записывать = Истина;
	
	Если УчетнаяПолитика.ПрименяетсяУСН(Организация, Дата) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Параметры.Вставить("Ссылка", Ссылка);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДокУслуги.Ссылка.Дата КАК Период,
		|	ДокУслуги.Ссылка.ДокументРеализации.Дата КАК ОснованиеДата,
		|	ДокУслуги.Ссылка.ДокументРеализации.Номер КАК ОснованиеНомер,
		|	ДокУслуги.Ссылка.Организация КАК Организация,
		|	ДокУслуги.Ссылка.Контрагент КАК Контрагент,
		|	ДокУслуги.Ссылка.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ДокУслуги.Сумма - ДокУслуги.СуммаДоИзменения КАК Графа5
		|ИЗ
		|	Документ.КорректировкаРеализации.Услуги КАК ДокУслуги
		|ГДЕ
		|	ДокУслуги.Ссылка = &Ссылка
		|	И ДокУслуги.Сумма - ДокУслуги.СуммаДоИзменения <> 0";
	
		Таблица = Запрос.Выполнить().Выгрузить();
		Для каждого СтрокаТЗ из Таблица Цикл
			
			Запись = Движения.КнигаУчетаДоходовИРасходов.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, СтрокаТЗ);
			
			Запись.Содержание = "Корректировка вознаграждения. Оплата от покупателя """ + Строка(СтрокаТЗ.Контрагент) + """ по договору """ + Строка(СтрокаТЗ.ДоговорКонтрагента) + """";
			Запись.РеквизитыПервичногоДокумента = Формат(СтрокаТЗ.ОснованиеДата, "ДФ=dd.MM.yyyy") + ", № " +  ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СтрокаТЗ.ОснованиеНомер, Истина, Ложь);
			
		КонецЦикла;	
		
	КонецЕсли;
	
	Движения.КнигаУчетаДоходовИРасходов.Записать();
	
КонецПроцедуры
