
////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЙ ПРОГРАММНЫЙ ИНТЕРФЕЙС

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УстановитьСостояниеДокумента();
	УстановитьВидимость(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Себестоимость = Объект.Сумма - Объект.СуммаКВ;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьСостояниеДокумента();
	Себестоимость = Объект.Сумма - Объект.СуммаКВ;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура СуммаПриИзменении(Элемент)
	
	Реквизиты = ПолучитьРеквизитыОперации(Объект.ВидОперации);
	Если Реквизиты.ФиксированнаяСебестоимость Тогда
		Объект.СуммаКВ = Объект.Сумма - Себестоимость;
	ИначеЕсли Реквизиты.СводныйУчет Тогда
	Иначе	
		Объект.СуммаКВ = Объект.Сумма * Объект.ПроцентКВ / 100;
		Себестоимость = Объект.Сумма - Объект.СуммаКВ;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцентКВПриИзменении(Элемент)
	
	Объект.СуммаКВ = Объект.Сумма * Объект.ПроцентКВ / 100;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьРеквизитыОперации(ВидОперации)
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидОперации, "ФиксированнаяСебестоимость, СводныйУчет, УчетПоДоговорам, УчетПоНоменклатуре, ПривязыватьКРеализации");
	
КонецФункции	

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимость(ЭтаФорма)
	
	Объект = ЭтаФорма.Объект;
	ЭтаФорма.Элементы.ВидОперации.ТолькоПросмотр = ЗначениеЗаполнено(Объект.Основание);	
	ЭтаФорма.Элементы.Реализация.ТолькоПросмотр  = ЗначениеЗаполнено(Объект.Основание);	
	ЭтаФорма.Элементы.Основание.Видимость        = ЗначениеЗаполнено(Объект.Основание);	
	
	РеквизитыОперации = ПолучитьРеквизитыОперации(Объект.ВидОперации);
	
	ЭтаФорма.Элементы.Контрагент.Видимость    = НЕ РеквизитыОперации.СводныйУчет;
	ЭтаФорма.Элементы.VIN.Видимость           = НЕ РеквизитыОперации.СводныйУчет;
	
	ЭтаФорма.Элементы.Номенклатура.Видимость       = РеквизитыОперации.УчетПоНоменклатуре;
	ЭтаФорма.Элементы.ГруппаРеализация.Видимость   = РеквизитыОперации.ПривязыватьКРеализации;
	ЭтаФорма.Элементы.ДоговорКонтрагента.Видимость = РеквизитыОперации.УчетПоДоговорам;
	ЭтаФорма.Элементы.ПроцентКВ.Видимость          = НЕ РеквизитыОперации.ФиксированнаяСебестоимость И НЕ РеквизитыОперации.СводныйУчет;
	ЭтаФорма.Элементы.Себестоимость.Видимость      = РеквизитыОперации.ФиксированнаяСебестоимость;
	
	ЭтаФорма.Элементы.ЗаполнитьОрганизацию.Видимость = (Объект.ВидОперации = ПредопределенноеЗначение("Справочник.АСЦ_ВидыОперацийПлановоеНачислениеКВ.КВБанков"));
	
КонецПроцедуры	

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	УстановитьВидимость(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОрганизацию(Команда)
	ЗаполнитьОрганизациюНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОрганизациюНаСервере()
	
	СтруктураКВ = РегистрыСведений.АСЦ_ПлановыеПроцентыКВ.ПолучитьСтруктуруПроцентаКВ(Объект.Комитент, Объект.ДЦ, Объект.Дата);
	Объект.Организация = СтруктураКВ.Организация;
	Объект.ПроцентКВ   = СтруктураКВ.ПроцентКВ;
	Объект.СуммаКВ     = Окр(Объект.Сумма * Объект.ПроцентКВ / 100, 2);
	
КонецПроцедуры

&НаСервере
Процедура НоменклатураПриИзмененииНаСервере()
	
	Если Объект.ВидОперации.ФиксированнаяСебестоимость Тогда
		
		ТаблицаЦен = Ценообразование.ПолучитьТаблицуЦенНоменклатуры(Объект.Номенклатура, Константы.ТипЦенПлановойСебестоимостиНоменклатуры.Получить(), Объект.Дата);
		Себестоимость = 0;
		Если ТаблицаЦен.Количество() > 0 Тогда
			Себестоимость = ТаблицаЦен[0].Цена;
		КонецЕсли;	
		Объект.СуммаКВ = Объект.Сумма - Себестоимость;
		
	КонецЕсли;	
	
	База = Справочники.ВнешниеИнформационныеБазы.НайтиПоНаименованию("Юникус");
	НастройкаСтатьиДоходов = Справочники.СоответствиеВнешнимИБ.НайтиПоНаименованию("Номенклатура <-> Статьи доходов",,, База.ТипБД);
	Объект.Статья = АСЦ_ПоискОбъектов.ПолучитьОбъектПоСоотвествию(Строка(Объект.Номенклатура), НастройкаСтатьиДоходов, База);
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	НоменклатураПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СебестоимостьПриИзменении(Элемент)
	
	Объект.СуммаКВ = Объект.Сумма - Себестоимость;
	
КонецПроцедуры
