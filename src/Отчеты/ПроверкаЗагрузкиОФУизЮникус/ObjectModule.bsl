
#Область ОписаниеОбработки

Функция СведенияОВнешнейОбработке() Экспорт

	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("2.2.2.1");
	
	// HKEY_LOCAL_MACHINE\SOFTWARE\Classes\ADODB.Connection
	Разрешение = РаботаВБезопасномРежиме.РазрешениеНаСозданиеCOMКласса("ADODB.Connection", "{00000514-0000-0010-8000-00AA006D2EA4}");
	ПараметрыРегистрации.Разрешения.Добавить(Разрешение);
	
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительныйОтчет();
	ПараметрыРегистрации.Версия = Метаданные().Комментарий;
	ПараметрыРегистрации.БезопасныйРежим = Истина;
	ПараметрыРегистрации.Информация = Метаданные().Представление();
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Представление = Метаданные().Представление();
	НоваяКоманда.Идентификатор = Метаданные().Имя;
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	НоваяКоманда.ПоказыватьОповещение = Ложь;
	
	Возврат ПараметрыРегистрации;

КонецФункции

#КонецОбласти

Функция вЧисло(Знач Парам)
	
	Парам = СтрЗаменить(Парам, ",", ".");
	
	Попытка
		Возврат Число(Парам);
	Исключение
		Возврат 0;
	КонецПопытки;	
	
КонецФункции	

Функция ПолучитьДанныеЮникус(АдресХранилища)
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xlsx");
 	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресХранилища);	
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.Прочитать(ИмяВременногоФайла);
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("Дата",          Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("Договор",       Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("Центр",         Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДанных.Колонки.Добавить("Сумма",         Новый ОписаниеТипов("Число"));
	ТаблицаДанных.Колонки.Добавить("СуммаКВ",       Новый ОписаниеТипов("Число"));
	ТаблицаДанных.Колонки.Добавить("СуммаЮникус",   Новый ОписаниеТипов("Число"));
	ТаблицаДанных.Колонки.Добавить("СуммаКВЮникус", Новый ОписаниеТипов("Число"));
	
	ТипБазы = Справочники.ТипыБазДанных.НайтиПоНаименованию("Юникус (Oracle)");
	База    = Справочники.ВнешниеИнформационныеБазы.НайтиПоНаименованию("Юникус");
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("НастройкаДепартаменты",  Справочники.СоответствиеВнешнимИБ.НайтиПоНаименованию("DEPARTMENTS <-> Организации",,, ТипБазы));
	
	КэшДанных = Новый Структура;
	КэшДанных.Вставить("Департамент", Новый Соответствие);
	
	Счетчик = 2;
	СчетчикПустых = 0;
	Пока Истина Цикл
		
		Счетчик = Счетчик + 1;
		
		Серия   = СокрЛП(ТабДок.Область(Счетчик, 13).Текст);
		Договор = СокрЛП(ТабДок.Область(Счетчик, 14).Текст); 
		Если ЗначениеЗаполнено(Серия)
			И НЕ СтрНачинаетсяС(Договор, Серия) Тогда
			Договор = Серия + Договор;
		КонецЕсли;	
				  
		Сумма   = вЧисло(ТабДок.Область(Счетчик, 10).Текст);		  
		СуммаКВ = вЧисло(ТабДок.Область(Счетчик, 19).Текст);		  
		ДЦ      = ТабДок.Область(Счетчик, 18).Текст;
		Дата    = АСЦ_ОбщийМодуль.вДату(ТабДок.Область(Счетчик, 23).Текст);
		
		Если ЗначениеЗаполнено(Договор)
			ИЛИ ЗначениеЗаполнено(Сумма) Тогда
			
			НоваяСтрока = ТаблицаДанных.Добавить();
			НоваяСтрока.Дата          = Дата;
			НоваяСтрока.Договор       = СтрЗаменить(Договор, " ", "");
			НоваяСтрока.СуммаЮникус   = Сумма;
			НоваяСтрока.СуммаКВЮникус = СуммаКВ;
			НоваяСтрока.Центр         = АСЦ_ПоискОбъектов.ПолучитьОбъектПоСоотвествию(ДЦ, ДопПараметры.НастройкаДепартаменты, База, КэшДанных.Департамент);
			
			СчетчикПустых = 0;
			
		Иначе
			
			СчетчикПустых = СчетчикПустых + 1;
			
		КонецЕсли;
		
		Если СчетчикПустых > 3 Тогда
			Прервать;
		КонецЕсли;	
		
	КонецЦикла;	
	
	Результат = Новый Структура;
	Результат.Вставить("Данные", ТаблицаДанных);
	//Результат.Вставить("Ошибки", ТаблицаОшибок);
	
	Возврат Результат;
	
КонецФункции	


Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	
	АдресХранилища = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("АдресХранилища")).Значение;
	
	ВнешниеНаборыДанных = ПолучитьДанныеЮникус(АдресХранилища);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);

	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки,);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);

	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры
