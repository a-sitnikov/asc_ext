
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	База = Справочники.ВнешниеИнформационныеБазы.НайтиПоНаименованию("Юникус");
	Вариант = "ID_UNICUS";
	
	Если ТипЗнч(Параметры.ОбъектыНазначения) = Тип("Массив") 
		И Параметры.ОбъектыНазначения.Количество() > 0 Тогда
		
		Документ = Параметры.ОбъектыНазначения[0];
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Документ) Тогда
		ДокументПриИзмененииНаСервере();
		ОбновитьНаСервере();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаСервере()
	
	Если Вариант = "ID_UNICUS" Тогда
		ТекстЗапроса =
		"SELECT
		|	*
		|FROM
		|	unicusweb.V_ASC_PAY_CONTRACT_HIST
		|WHERE
		|	PAY_ID = " + Формат(ID, "ЧГ=0") + "
		|ORDER BY
		|	PAY_DATE Desc";
	Иначе	
		ТекстЗапроса =
		"SELECT
		|	*
		|FROM
		|	unicusweb.V_ASC_PAY_CONTRACT_HIST
		|WHERE
		|	POLICY_NUMBER = '" + СокрЛП(Договор) + "'
		|ORDER BY
		|	PAY_DATE Desc";
	КонецЕсли;
	
	СтрокаСоединения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(База, "СтрокаПодключения");
	
	Соединение = Новый COMОбъект("ADODB.Connection");
	Соединение.Open(СтрокаСоединения);
	
	Таблица = АСЦ_ОбщийМодуль.ВыполнитьЗапросADO(Соединение, ТекстЗапроса);
	ТалицаОплаты.Загрузить(Таблица);
	
	МассивОшибокContact = Новый Массив;
	Счетчик = 0;
	Для каждого СтрокаТЗ из ТалицаОплаты Цикл
		
		Счетчик = Счетчик + 1;
		СтрокаТЗ.НомерСтроки = Счетчик;
		СтрокаТЗ.Ошибка = 1;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТЗ.AGENT) Тогда
			МассивОшибокContact.Добавить("В строке " + Счетчик + " не заполнена организация"); 
			СтрокаТЗ.Ошибка = 0;
		КонецЕсли;	
		
	КонецЦикла;	
	
	Если МассивОшибокContact.Количество() > 0 Тогда
		ТекстОшибкиContact = СтрСоединить(МассивОшибокContact, Символы.ПС);
	КонецЕсли;	
	Элементы.ТекстОшибкиContact.Видимость = (МассивОшибокContact.Количество() > 0);
	
	МассивPayID = Новый Массив;
	Для каждого СтрокаТЗ из ТалицаОплаты Цикл
		Если ЗначениеЗаполнено(СтрокаТЗ.PAY_ID) Тогда
			МассивPayID.Добавить(Формат(СтрокаТЗ.PAY_ID, "ЧГ=0"));
		КонецЕсли;	
	КонецЦикла;	
	
	Если МассивPayID.Количество() > 0 Тогда
		
		PayId = СтрСоединить(МассивPayID, ", ");
		
		ТекстЗапроса =
		"SELECT
		|	*
		|FROM
		|	unicusweb.V_ASC_ACT
		|WHERE
		|	ACT_ID IN 
		|		(SELECT 
		|			ACT_ID
		|		 FROM
		|			unicusweb.V_ASC_ACT
		|		 WHERE
		|			PAY_ID IN (" + PayID + "))
		|	AND (COL1 IS NOT NULL 
		|		 OR PAY_ID IN (" + PayID + "))";
		
		СтрокаСоединения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(База, "СтрокаПодключения");
		
		Соединение = Новый COMОбъект("ADODB.Connection");
		Соединение.Open(СтрокаСоединения);
		
		Таблица = АСЦ_ОбщийМодуль.ВыполнитьЗапросADO(Соединение, ТекстЗапроса);
		ТаблицаАкты.Загрузить(Таблица);
		
		Для каждого СтрокаТЗ из ТаблицаАкты Цикл
			
			СтрокаТЗ.Ошибка = 1;
			
			Если НЕ ЗначениеЗаполнено(СтрокаТЗ.PAY_ID)
				И НЕ ЗначениеЗаполнено(СтрокаТЗ.DATE_SIGN) Тогда
				
				СтрокаТЗ.Ошибка = 0;
				
			КонецЕсли;	
			
		КонецЦикла;	
		
	КонецЕсли;
	
	Элементы.ТаблицаАкты.ТекущаяСтрока = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура ДокументПриИзмененииНаСервере()
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, "ВнешнийИД, ДоговорКонтрагента");
	Договор = Реквизиты.ДоговорКонтрагента;
	ID      = Реквизиты.ВнешнийИД;
	
	Если НЕ ЗначениеЗаполнено(Реквизиты.ВнешнийИД) Тогда
		Вариант = "Договор";
		Заголовок = "Юникус: " + Договор;
	Иначе	
		Заголовок = "Юникус: " + ID;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументПриИзменении(Элемент)
	ДокументПриИзмененииНаСервере();
КонецПроцедуры



