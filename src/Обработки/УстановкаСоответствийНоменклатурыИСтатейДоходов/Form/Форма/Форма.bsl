
&НаКлиенте
Процедура Прочитать(Команда)
	ПрочитатьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Родитель", Справочники.Номенклатура.НайтиПоНаименованию("Страховки"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Номенклатура.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Родитель = &Родитель";
	
	ТаблицаДанных.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Кэш = Новый Соответствие;
	Для каждого СтрокаТЗ из ТаблицаДанных Цикл
		СтрокаТЗ.Статья = АСЦ_ПоискОбъектов.ПолучитьОбъектПоСоотвествию(СтрокаТЗ.Наименование, Объект.Настройка, Объект.База, Кэш);
	КонецЦикла;	
	
	Элементы.ТаблицаДанныхЗаписать.КнопкаПоУмолчанию = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;	
	
	ЗаписатьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере()
	
	Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "НаименованиеUNICUS");
	
	Для каждого СтрокаТЗ из ТаблицаДанных Цикл
		
		Если НЕ СтрокаТЗ.Изменен Тогда
			Продолжить;
		КонецЕсли;	
		
		Если НЕ ЗначениеЗаполнено(СтрокаТЗ.Статья) Тогда
			Продолжить;
		КонецЕсли;	
		
		НаборЗаписей = РегистрыСведений.СоответствиеОбъектовТекущейИВнешнихИБ.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ИспользуемаяИБ.Установить(Объект.База, Истина);
		НаборЗаписей.Отбор.ОбъектВнешнейИБ.Установить(СтрокаТЗ.Наименование, Истина);
		НаборЗаписей.Отбор.НастройкаСоответствия.Установить(Объект.Настройка, Истина);
		
		Запись = НаборЗаписей.Добавить();
		Запись.ОбъектТекущейИБ = СтрокаТЗ.Статья;
		Запись.ИспользуемаяИБ  = Объект.База;
		Запись.ОбъектВнешнейИБ = СтрокаТЗ.Наименование;
		Запись.НастройкаСоответствия = Объект.Настройка;
		
		НаборЗаписей.Записать();
		
		СтрокаТЗ.Изменен = Ложь;
		
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДанныхОрганизацияПриИзменении(Элемент)
	
	Элементы.ТаблицаДанных.ТекущиеДанные.Изменен = Истина;
	
КонецПроцедуры

&НаСервере
Процедура БазаПриИзмененииНаСервере()
	
	Объект.ТипБазы = Объект.База.ТипБД;
	
КонецПроцедуры

&НаКлиенте
Процедура БазаПриИзменении(Элемент)
	БазаПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.База = Справочники.ВнешниеИнформационныеБазы.НайтиПоНаименованию("Юникус");
	Объект.Настройка = Справочники.СоответствиеВнешнимИБ.НайтиПоНаименованию("Номенклатура <-> Статьи доходов",,, Объект.База.ТипБД);
	
	Если НЕ ПравоДоступа("Изменение", Метаданные.РегистрыСведений.СоответствиеОбъектовТекущейИВнешнихИБ) Тогда
		ТолькоПросмотр = Истина;
	Конецесли;	
	
	СвойствоСтатья = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "Статья");
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаЗаполнить(Команда)
	ПроверкаЗаполнитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПроверкаЗаполнитьНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("База",                 Объект.База);
	Запрос.Параметры.Вставить("Настройка",            Объект.Настройка);
	Запрос.Параметры.Вставить("СвойствоСтатья",       СвойствоСтатья);
	Запрос.Параметры.Вставить("СвойствоНоменклатура", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "Номенклатура"));
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 10000
	|	Док.Ссылка КАК Документ,
	|	Док.Статья КАК Статья,
	|	Рег.ОбъектТекущейИБ КАК СтатьяНовая,
	|	Док.Номенклатура КАК Номенклатура
	|ИЗ
	|	Документ.АСЦ_ПлановоеНачислениеКВ КАК Док
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеОбъектовТекущейИВнешнихИБ КАК Рег
	|		ПО (Рег.ИспользуемаяИБ = &База)
	|			И (Рег.НастройкаСоответствия = &Настройка)
	|			И Док.Номенклатура.Наименование = Рег.ОбъектВнешнейИБ
	|ГДЕ
	|	НЕ Док.ПометкаУдаления
	|	И Док.Статья <> Рег.ОбъектТекущейИБ";
	
	ТЗ = Запрос.Выполнить().Выгрузить();
	ТаблицаПроверки.Загрузить(ТЗ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаИзменитьСтатью(Команда)
	
	ПроверкаИзменитьСтатьюНаСервере();
	Состояние("Статья изменена");
	
КонецПроцедуры

&НаСервере
Процедура ПроверкаИзменитьСтатьюНаСервере()
	
	Для каждого СтрокаТЗ из ТаблицаПроверки Цикл
		
		ДокументОбъект = СтрокаТЗ.Документ.ПолучитьОбъект();
		ДокументОбъект.Статья = СтрокаТЗ.СтатьяНовая;
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПроверкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = ТаблицаПроверки.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ПоказатьЗначение(, ТекущиеДанные.Документ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьНоменклатураПоУмолчанию(Команда)
	ПрочитатьНоменклатураПоУмолчаниюНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНоменклатураПоУмолчаниюНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Родитель", Справочники.СтатьиДоходовИРасходов.НайтиПоНаименованию("017. Финансовые услуги"));
	Запрос.Параметры.Вставить("Свойство", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "Номенклатура"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Спр.Ссылка КАК Статья,
	|	ВЫРАЗИТЬ(ДополнительныеСведения.Значение КАК Справочник.Номенклатура) КАК Номенклатура
	|ИЗ
	|	Справочник.СтатьиДоходовИРасходов КАК Спр
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|		ПО Спр.Ссылка = ДополнительныеСведения.Объект
	|			И (ДополнительныеСведения.Свойство = &Свойство)
	|ГДЕ
	|	Спр.Родитель = &Родитель
	|
	|УПОРЯДОЧИТЬ ПО
	|	Спр.Наименование";
	
	Таблица = Запрос.Выполнить().Выгрузить();
	ТаблицаНоменклатураПоУмолчанию.Загрузить(Таблица);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНоменклатураПоУмолчаниюНаСервере()
	
	Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "Номенклатура");
	
	Для каждого СтрокаТЗ из ТаблицаНоменклатураПоУмолчанию Цикл
		
		Если НЕ СтрокаТЗ.Изменен Тогда
			Продолжить;
		КонецЕсли;	
		
		Если НЕ ЗначениеЗаполнено(СтрокаТЗ.Статья) Тогда
			Продолжить;
		КонецЕсли;	
		
		Запись = РегистрыСведений.ДополнительныеСведения.СоздатьМенеджерЗаписи();
		Запись.Объект   = СтрокаТЗ.Статья;
		Запись.Свойство = Свойство;
		Запись.Значение = СтрокаТЗ.Номенклатура;
		Запись.Записать();
		
		СтрокаТЗ.Изменен = Ложь;
		
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНоменклатураПоУмолчанию(Команда)
	ЗаписатьНоменклатураПоУмолчаниюНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНоменклатураПоУмолчаниюНоменклатураПриИзменении(Элемент)
	
	Элементы.ТаблицаНоменклатураПоУмолчанию.ТекущиеДанные.Изменен = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПроверкаНоменклаутрыЗаполнитьНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Родитель", Справочники.СтатьиДоходовИРасходов.НайтиПоНаименованию("017. Финансовые услуги"));
	Запрос.Параметры.Вставить("Свойство", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "Номенклатура"));
	
	ВидОперации = Новый Массив;
	ВидОперации.Добавить(Справочники.АСЦ_ВидыОперацийПлановоеНачислениеКВ.Страховки);
	ВидОперации.Добавить(Справочники.АСЦ_ВидыОперацийПлановоеНачислениеКВ.ПрямыеРасчетыССК);
	ВидОперации.Добавить(Справочники.АСЦ_ВидыОперацийПлановоеНачислениеКВ.ОплатаВБанке);
	Запрос.Параметры.Вставить("ВидОперации", ВидОперации);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Док.Ссылка КАК Документ,
	|	ВЫРАЗИТЬ(ДополнительныеСведения.Значение КАК Справочник.Номенклатура) КАК Номенклатура,
	|	Док.Статья КАК Статья
	|ИЗ
	|	Документ.АСЦ_ПлановоеНачислениеКВ КАК Док
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|		ПО Док.Статья = ДополнительныеСведения.Объект
	|			И (ДополнительныеСведения.Свойство = &Свойство)
	|ГДЕ
	|	Док.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И Док.Статья <> ЗНАЧЕНИЕ(Справочник.СтатьиДоходовИРасходов.ПустаяСсылка)
	|	И НЕ Док.ПометкаУдаления
	|	И Док.ВидОперации В(&ВидОперации)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Док.Дата";
	
	Таблица = Запрос.Выполнить().Выгрузить();
	ТаблицаПроверкиНоменклатуры.Загрузить(Таблица);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаНоменклаутрыЗаполнить(Команда)
	ПроверкаНоменклаутрыЗаполнитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПроверкаИзменитьНоменклатуруНаСервере()
	
	Для каждого СтрокаТЗ из ТаблицаПроверкиНоменклатуры Цикл
		
		ДокументОбъект = СтрокаТЗ.Документ.ПолучитьОбъект();
		ДокументОбъект.Номенклатура = СтрокаТЗ.Номенклатура;
		
		Если ДокументОбъект.Проведен Тогда
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Иначе
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаИзменитьНоменклатуру(Команда)
	ПроверкаИзменитьНоменклатуруНаСервере();
КонецПроцедуры
