#Область ОписаниеОбработки

Функция СведенияОВнешнейОбработке() Экспорт
	
	//ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("2.1.3.1");
	//ПараметрыРегистрации.БезопасныйРежим = Ложь;
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("2.2.2.1");
	
	// HKEY_LOCAL_MACHINE\SOFTWARE\Classes\ADODB.Connection
	Разрешение = РаботаВБезопасномРежиме.РазрешениеНаСозданиеCOMКласса("ADODB.Connection", "{00000514-0000-0010-8000-00AA006D2EA4}");
	ПараметрыРегистрации.Разрешения.Добавить(Разрешение);
	
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительнаяОбработка();
	ПараметрыРегистрации.Версия = Метаданные().Комментарий;
	ПараметрыРегистрации.Информация = "Загрузка документов Реализация из UNICUS";
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Представление = Метаданные().Представление() + " - Открыть форму";
	НоваяКоманда.Идентификатор = Метаданные().Имя + "Форма";
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	НоваяКоманда.ПоказыватьОповещение = Ложь;
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Представление = "Загрузить реализации";
	НоваяКоманда.Идентификатор = "ЗагрузитьРеализации";
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
	НоваяКоманда.ПоказыватьОповещение = Ложь;
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

Процедура ВыполнитьКоманду(ИдентификаторКоманды, ПараметрыКоманды = Неопределено) Экспорт 
	
	Параметры = Новый Структура;
	
	База = ХранилищеОбщихНастроек.Загрузить("ЗагрузкаДокументовИзUnicus", "База",, "ЗагрузкаДокументовИзUnicus");
	Если НЕ ЗначениеЗаполнено(База) Тогда
		База = Справочники.ВнешниеИнформационныеБазы.НайтиПоНаименованию("Юникус");
	КонецЕсли;
	
	Параметры.Вставить("База",        База);
	Параметры.Вставить("ДатаНач",     ХранилищеОбщихНастроек.Загрузить("ЗагрузкаДокументовИзUnicus", "ДатаПоследнегоДокументаОплатаВБанке",, "ЗагрузкаДокументовИзUnicus"));
	Параметры.Вставить("ДатаКон",     '2100-01-01');
	Параметры.Вставить("Организация", Неопределено);
	Параметры.Вставить("ПерезаполнятьДокументы",   Ложь);
	Параметры.Вставить("ПерезаполнятьДоговоры",    Ложь);
	Параметры.Вставить("ВыводитьСообщения",        Ложь);
	Параметры.Вставить("ВыводитьСообщения",        Ложь);
	Параметры.Вставить("ОтложенноеПроведение",     Истина);
	Параметры.Вставить("ЗагружатьДокументыОплаты", Ложь);
	Параметры.Вставить("ВариантЗагрузки",          "ОтПоследнейДаты");
	
	ЗагрузитьРеализации(Параметры);
	
КонецПроцедуры	

#КонецОбласти

#Область ПолучениеДанных

Функция ПолучитьТекстЗапроса(ДатаНач, ДатаКон, Параметры) Экспорт
	
	ТекстЗапроса =
	"SELECT * FROM (
	|SELECT
	|	CASE WHEN tab.contract_status_name = 'Аннулированный договор'
	|		THEN 0
	|		ELSE tab.FULL_PREMIUM 
	|	END as FULL_PREMIUM,
	|	CASE WHEN tab.contract_status_name = 'Аннулированный договор'
	|		THEN 0
	|		ELSE tab.PREMIUM_SUM 
	|	END as PREMIUM_SUM,
	|	tab.KV as KV,
	|	CASE WHEN tab.contract_status_name = 'Аннулированный договор'
	|		THEN 0
	|		ELSE tab.KV_RUB 
	|	END as KV_RUB,
	|	CASE WHEN tab.INSUR_TYPE = 'пролонгация' 
	|		THEN 1
	|		ELSE 0
	|	END as Пролонгация,
	|	tab.TS_NEW as TS_NEW,
	|	tab.STORONNIY_CLIENT as STORONNIY_CLIENT,
	// Номенклатура
	|	tab.PRODUCT_NAME as Номенклатура,
	// ТС
	|	TRIM(tab.VIN) as VIN,
	|	tab.TRANSPORT_MARK as TRANSPORT_MARK,
	|	tab.TRANSPORT_MODEL as TRANSPORT_MODEL,
	|	TO_NUMBER(tab.TRANSPORT_OUT_DATE) as TRANSPORT_OUT_DATE,
	// Контрагент
	|	tab.EKB_ID_C as КонтрагентГУИД,
	|	tab.SUBJECT_NAME as Контрагент,
	|	TRIM(tab.INSURER_INN) as КонтрагентИНН,
	|	SUBSTR(tab.INSURER_KPP, 1, 9) as КонтрагентКПП,
	|	tab.INSURER_DOC_SERIES as КонтрагентПаспортСерия,
	|	tab.INSURER_DOC_NUMBER as КонтрагентПаспортНомер,
	|	tab.INSURER_DATE_OUT as КонтрагентПаспортДата,
	|	tab.INSURER_PLACE_OUT as КонтрагентПаспортВыдан,
	// Договор
	|	tab.POLICY_NUMBER as Договор,
	// Даты, время отбрасываем
	// Для оплат в банке нет даты оплаты, тк нет и самой оплаты
	|	TRUNC(tab.DATE_SIGN) as Дата,
	|	TRUNC(tab.DATE_SIGN) as ДоговорДата,
	|	TRUNC(tab.ACTION_BEGIN_DATE) as ДатаНачала,
	|	TRUNC(tab.ACTION_END_DATE) as СрокДействия,
	// Страховая
	|	TRIM(tab.SK_CODE) as СтраховаяГУИД,
	|	TRIM(tab.SK_NAME) as Страховая,
	|	TRIM(tab.SK_INN) as СтраховаяИНН,
	|	SUBSTR(tab.SK_KPP, 1, 9) as СтраховаяКПП,
	// Подразделение
	|	tab.DEPARTMENT_NAME as ДЦ,
	// Банк
	|	tab.BANK as Банк,
	|	tab.BANK_BIK as БанкБИК,
	// Организация
	|	tab.AGENT as Организация,
	|	TRIM(tab.AGENT_INN) as ОрганизацияИНН,
	|	SUBSTR(tab.AGENT_KPP, 1, 9) as ОрганизацияКПП,
	|	tab.JP_ACCOUNT_PERS as СчетОрганизации,
	// Тип: Сторонний, АМ коммерч, АМ корпор, АМ бу, АМ розница
	|	tab.TSFO as ТипПродажи,
	// Тип платежа: Наличные, Безналичный, Эквайринг, Прямые расчеты с СК, Оплата в Банке
	|	tab.PAY_TYPE_CONTRACT as ТипПлатежа,
	|	CASE WHEN tab.contract_status_name = 'Аннулированный договор'
	|		THEN 0
	|		ELSE tab.PAY_AMOUNT 
	|	END as СуммаОплаты,
	|	NVL(tab.DATE_UPDATE, tab.DATE_SIGN) as ДатаИзменения,
	|	tab.PAY_ID as ID,
	|	tab.DIRECTION_CODE as IDПроекта,
	|	tab.INVOICE_ID as IDСчета
	|FROM 
	|	unicusweb.v_asc_pay_contract_hist tab
	|WHERE
	|	tab.PAY_TYPE_CONTRACT = 'Оплата в Банке'
	// Диагностическая карта будет вводиться вручную
	|	AND tab.PRODUCT_NAME <> 'Диагностическая карта'
	|) tab
	|WHERE 
	|	tab.Дата IS NOT NULL
	|	AND tab.Дата >= &МинДата";
	
	Если Параметры.ВариантЗагрузки = "ПериодОплаты" Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	AND tab.Дата BETWEEN &ДатаНач AND &ДатаКон";
		
	ИначеЕсли Параметры.ВариантЗагрузки = "ПериодИзменений" Тогда	
		ТекстЗапроса = ТекстЗапроса + "
		|	AND NVL(tab.ДатаИзменения, tab.Дата) BETWEEN &ДатаНач AND &ДатаКон";
	Иначе
		ТекстЗапроса = ТекстЗапроса + "
		|	AND (tab.ДатаИзменения BETWEEN &ДатаНач AND &ДатаКон 
		|     OR tab.Дата BETWEEN &ДатаНач AND &ДатаКон)";
	КонецЕсли;	
	
	Если Параметры.Свойство("ОтборДоговор") Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	AND UPPER(tab.Договор) = '" + ВРег(СокрЛП(Параметры.ОтборДоговор)) + "'";
	КонецЕсли;	
	
	Если Параметры.Свойство("ОтборVIN") Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	AND UPPER(tab.VIN) = '" + ВРег(СокрЛП(Параметры.ОтборVIN)) + "'";
	КонецЕсли;	
	
	Если Параметры.Свойство("ОтборID") Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	AND tab.ID = " + Формат(Параметры.ОтборID, "ЧГ=0") + "";
	КонецЕсли;	
	
	Если Параметры.Свойство("ОтборПродукт") Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	AND tab.Номенклатура = '" + Строка(Параметры.ОтборПродукт) + "'";
	КонецЕсли;	
	
	Если Параметры.Свойство("ОтборОрганизация") Тогда
		
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.ОтборОрганизация, "ИНН,КПП");
		ТекстЗапроса = ТекстЗапроса + "
		|	AND tab.ОрганизацияИНН = '" + Реквизиты.ИНН + "'";
		
		Если ЗначениеЗаполнено(Реквизиты.КПП) Тогда
			
			ТекстЗапроса = ТекстЗапроса + "
			|	AND tab.ОрганизацияКПП = '" + Реквизиты.КПП + "'";
			
		КонецЕсли;	
		
	КонецЕсли;	
	
	Если Параметры.Свойство("ИмяТаблицы") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "unicusweb.v_asc_pay_contract_hist", Параметры.ИмяТаблицы);
	КонецЕсли;	
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|ORDER BY
	|	ДатаИзменения, СуммаОплаты";
	
	// Отладочные отборы
	//Доп отбор для тестирования
	// например AND rownum <= 10  - выбираем только первые 10 записей
	Если Параметры.Свойство("Тест_КоличествоСтрок") Тогда
		
		ТекстЗапроса =  
		"SELECT
		| *
		|FROM (
		|" + ТекстЗапроса + "
		|)
		|WHERE
		|	rownum <= " + Формат(Параметры.Тест_КоличествоСтрок, "ЧГ=0");
		
	КонецЕсли;	
	
	МинДата = '2018-01-01';
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&МинДата", ИнтеграцияСВнешнимиСистемамиУХ.АСЦ1_асцПолучитьТекстПараметра(МинДата, "Oracle"));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДатаНач", ИнтеграцияСВнешнимиСистемамиУХ.АСЦ1_асцПолучитьТекстПараметра(ДатаНач, "Oracle"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДатаКон", ИнтеграцияСВнешнимиСистемамиУХ.АСЦ1_асцПолучитьТекстПараметра(ДатаКон, "Oracle"));
	
	Возврат ТекстЗапроса;
	
КонецФункции	

Функция ПолучитьТаблицуДанныхРеализации(База, ДатаНач, ДатаКон, Параметры ) Экспорт
	
	ТекстЗапроса = ПолучитьТекстЗапроса(ДатаНач, ДатаКон, Параметры);
	СтрокаСоединения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(База, "СтрокаПодключения");
	
	Соединение = Новый COMОбъект("ADODB.Connection");
	Соединение.Open(СтрокаСоединения);
	
	Возврат АСЦ_ОбщийМодуль.ВыполнитьЗапросADO(Соединение, ТекстЗапроса);
	
Конецфункции	

Функция ПолучитьТаблицуДанныхРеализацииТест(Знач Макет = Неопределено) Экспорт
	
	// Для тестирования разных ситуаций подготовим таблицу вручную
	Если Макет = Неопределено Тогда
		Макет = ПолучитьМакет("ТестовыеДанные");
	КонецЕсли;	
	
	ТипыКолонок = Новый Соответствие;
	
	ТаблицаЗначений = Новый ТаблицаЗначений;
	Для Счетчик = 2 По Макет.ШиринаТаблицы Цикл
		
		ТипСтр = Макет.Область(1, Счетчик).Текст;
		Имя = Макет.Область(2, Счетчик).Текст;
		Если ТипСтр = "Дата" Тогда
			Тип = Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя));
		ИначеЕсли ТипСтр = "Число" Тогда	
			
			Массив = Новый Массив;
			Массив.Добавить(Тип("Null"));
			Массив.Добавить(Тип("Число"));
			
			Тип = Новый ОписаниеТипов(Массив);
			
		Иначе
			Тип = Неопределено;
		КонецЕсли;
		
		ТипыКолонок.Вставить(Имя, ТипСтр);
		
		ТаблицаЗначений.Колонки.Добавить(Имя, Тип);
		
	КонецЦикла;	
	
	Для СчетчикСтрок = 3 По Макет.ВысотаТаблицы Цикл
		
		// В первой колонке пометка
		Пометка = Макет.Область(СчетчикСтрок, 1).Текст;
		Если НЕ ЗначениеЗаполнено(Пометка) Тогда
			Продолжить;
		КонецЕсли;	
		
		НоваяСтрока = ТаблицаЗначений.Добавить();
		
		Для каждого Колонка из ТаблицаЗначений.Колонки Цикл
			
			СчетчикКолонок = ТаблицаЗначений.Колонки.Индекс(Колонка) + 2;
			Значение = Макет.Область(СчетчикСтрок, СчетчикКолонок).Текст;
			
			Если ТипыКолонок[Колонка.Имя] = "Дата" Тогда
				Если СтрДлина(Значение) = 8 Тогда
					Массив = СтрРазделить(Значение, ".");
					НоваяСтрока[Колонка.Имя] = Дата(2000 + Число(Массив[2]), Число(Массив[1]), Число(Массив[0]));
				Иначе	
					НоваяСтрока[Колонка.Имя] = Дата(Значение);
				КонецЕсли;	
				
			ИначеЕсли ТипыКолонок[Колонка.Имя] = "Число" Тогда
				
				Если ЗначениеЗаполнено(Значение) Тогда
					НоваяСтрока[Колонка.Имя] = Число(Значение);
				Иначе	
					НоваяСтрока[Колонка.Имя] = Null;
				КонецЕсли;	
				
			Иначе
				НоваяСтрока[Колонка.Имя] = Значение;
			КонецЕсли;	
			
		КонецЦикла;
		
	КонецЦикла;	
	
	Возврат ТаблицаЗначений;
	
Конецфункции	

#КонецОбласти

#Область СлужебныеФункции

Функция ЕстьNULL(Парам, Значение)
	Возврат ?(ЗначениеЗаполнено(Парам), Парам, Значение);
КонецФункции	

Функция вДату(Парам)
	
	Массив = СтрРазделить(Парам, ".");
	Возврат Дата(Число(Массив[2]), Число(Массив[1]), Число(Массив[0]));
	
КонецФункции	

Функция ЗаписатьДокумент(ДокОбъект, Режим, Сообщать = Истина, ОтложенноеПроведение = Ложь)
	
	Результат = Истина;	
	
	Если ДокОбъект = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;	
	
	// тк некуда записывать пропущенные документы, то будем просто останавливать обработку
	Если ОтложенноеПроведение Тогда
		
		НачатьТранзакцию();
		
		ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
		
		Запись = РегистрыСведений.ОтложенныеДвиженияДокументов.СоздатьМенеджерЗаписи();
		Запись.Документ      = ДокОбъект.Ссылка;
		Запись.УзелОбмена    = ПланыОбмена.Полный.ЭтотУзел();
		Запись.ДатаДокумента = ДокОбъект.Дата;
		Запись.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Иначе	
		ДокОбъект.Записать(Режим);
	КонецЕсли;	
	
	Если Сообщать Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Строка(ДокОбъект);
		Сообщение.КлючДанных = ДокОбъект.Ссылка;
		Сообщение.Сообщить();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции	

Функция ЗначенияОтличаются(Структруа1, Структура2)
	
	Для каждого КлючИЗначение Из Структруа1 Цикл
		
		Если КлючИЗначение.Значение <> Структура2[КлючИЗначение.Ключ] Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;	
	
	Возврат Ложь;
	
КонецФункции	

Процедура ДобавитьИзменениеОбъекта(ДокументСсылка, База)
	
	Если НЕ ЗначениеЗаполнено(База) Тогда
		Возврат;
	КонецЕсли;	
	
	Запись = РегистрыСведений.ИзмененныеОбъектыДляВыгрузки.СоздатьМенеджерЗаписи();
	Запись.ИспользуемаяИБ        = База;
	Запись.НастройкаСоответствия = Справочники.СоответствиеВнешнимИБ.НайтиПоНаименованию("Реализация ОФУ");
	Запись.Элемент               = ДокументСсылка;
	
	Запись.Записать();
	
КонецПроцедуры	

Функция ПолучитьДатуЗапретаИзменения(Организация, Кэш)
	
	ДатаЗапрета = Кэш[Организация];
	Если ДатаЗапрета <> Неопределено Тогда
		Возврат ДатаЗапрета;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Организация", Организация);
	Запрос.Параметры.Вставить("Раздел", ПланыВидовХарактеристик.РазделыДатЗапретаИзменения.НайтиПоНаименованию("Учет по МСФО"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	КОНЕЦПЕРИОДА(ДатыЗапретаИзменения.ДатаЗапрета, ДЕНЬ) КАК ДатаЗапрета
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|ГДЕ
	|	ДатыЗапретаИзменения.Объект = &Организация
	|	И ДатыЗапретаИзменения.Пользователь = ЗНАЧЕНИЕ(Перечисление.ВидыНазначенияДатЗапрета.ДляВсехПользователей)
	|	И ДатыЗапретаИзменения.Раздел = &Раздел
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	КОНЕЦПЕРИОДА(ДатыЗапретаИзменения.ДатаЗапрета, ДЕНЬ)
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|ГДЕ
	|	ДатыЗапретаИзменения.Объект = &Раздел
	|	И ДатыЗапретаИзменения.Пользователь = ЗНАЧЕНИЕ(Перечисление.ВидыНазначенияДатЗапрета.ДляВсехПользователей)
	|	И ДатыЗапретаИзменения.Раздел = &Раздел
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ДатаЗапрета = '0001-01-01';
	Иначе
		ДатаЗапрета = Результат.Выгрузить()[0].ДатаЗапрета;
	КонецЕсли;	
	
	Кэш.Вставить(Организация, ДатаЗапрета);
	Возврат ДатаЗапрета;
	
КонецФункции

Процедура СохранитьДатуПоследнегоДокумента(ДатаИзменения)
	
	ХранилищеОбщихНастроек.Сохранить("ЗагрузкаДокументовИзUnicus", "ДатаПоследнегоДокументаОплатаВБанке", ДатаИзменения,, "ЗагрузкаДокументовИзUnicus");
	
КонецПроцедуры	

Функция ДействиеПоДатеЗапрета(Дата, ДатаИзменения, Организация, Кэш)
	
	Результат = Новый Структура;
	Результат.Вставить("ДатаЗапрета", ПолучитьДатуЗапретаИзменения(Организация, Кэш));
	
	Если Дата > Результат.ДатаЗапрета Тогда
		//_____[ДЗ]____Д___ДИ___
		Результат.Вставить("Имя", "Редактирование");
	Иначе	
		
		Если ДатаИзменения > Результат.ДатаЗапрета Тогда
			//_____Д____[ДЗ]___ДИ___
			Результат.Вставить("Имя", "Корректировка");
		Иначе
			//_____Д____ДИ___[ДЗ]___
			Результат.Вставить("Имя", "ИзменениеЗакрытогоПериода");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции	

#КонецОбласти

#Область ПоискДанных

Функция НайтиДокументПоДоговору(Договор)
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Договор", Договор);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Док.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.АСЦ_ПлановоеНачислениеКВ КАК Док
	|ГДЕ
	|	Док.ДоговорКонтрагента = &Договор
	|	И НЕ Док.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Док.Дата УБЫВ";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		ДокументСсылка = Результат.Выгрузить()[0][0];
	Иначе
		ДокументСсылка = Неопределено;
	КонецЕсли;
	
	Возврат ДокументСсылка;
	
КонецФункции	

Функция ПолучитьСуммы(Данные)
	
	// Возьмем КВ пропорционально оплате
	Результат = Новый Структура;
	
	// В некоторых полисах PREMIUM_SUM не заполнено
	// пример 001АТ-17/0015125
	Результат.Вставить("ПолнаяСумма", Данные.PREMIUM_SUM);
	Если НЕ ЗначениеЗаполнено(Результат.ПолнаяСумма) Тогда
		Результат.ПолнаяСумма = Данные.FULL_PREMIUM;
	КонецЕсли;	
	
	Данные.KV_RUB      = ЕстьNULL(Данные.KV_RUB, 0);
	Данные.СуммаОплаты = ЕстьNULL(Данные.СуммаОплаты, 0);
	
	Если Данные.ТипПлатежа = "Оплата в Банке" Тогда
		Данные.СуммаОплаты = Результат.ПолнаяСумма;
	КонецЕсли;	
	
	// Расчитаем пропорционально оплате
	//Результат.Вставить("СуммаВыручка",  Окр(Данные.СуммаОплаты * Данные.KV_RUB / Результат.ПолнаяСумма, 2));
	
	// Сумма КВ уже расчитана
	Результат.Вставить("СуммаКВ",       Мин(Данные.KV_RUB, Данные.СуммаОплаты));
	Результат.Вставить("СуммаАгентсие", Данные.СуммаОплаты - Результат.СуммаКВ);
	
	Возврат Результат;
	
КонецФункции	

Функция ПолучитьСтруктуруСсылок(Данные, Параметры, База, КэшДанных, КэшСообщений) Экспорт
	
	СтруктураСсылок = Новый Структура;
	
	// Организация
	Данные.ОрганизацияИНН = ЕстьNULL(Данные.ОрганизацияИНН, "");
	Данные.ОрганизацияКПП = ЕстьNULL(Данные.ОрганизацияКПП, "");
	
	СтруктураСсылок.Вставить("Организация", АСЦ_ПоискОбъектов.ПолучитьОрганизациюПоИНН(Данные.ОрганизацияИНН, Данные.ОрганизацияКПП, КэшДанных.Организация));
	
	Если НЕ ЗначениеЗаполнено(СтруктураСсылок.Организация) Тогда
		
		Если КэшСообщений.Организация[Данные.ОрганизацияИНН + "/" + Данные.ОрганизацияКПП] = Неопределено Тогда
			
			ТекстОшибки = "Не найдена организация: " + ЕстьNULL(Данные.Организация, "") + ", ИНН: " + Данные.ОрганизацияИНН + ", КПП: " + Данные.ОрганизацияКПП + ", id: " + Данные.id;
			ЗаписьЖурналаРегистрации("Загрузка реализаций из Unicus",
			УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
			
			Сообщить(ТекстОшибки);
			КэшСообщений.Организация.Вставить(Данные.ОрганизацияИНН + "/" + Данные.ОрганизацияКПП, Истина);
			
		КонецЕсли;
		
		Возврат СтруктураСсылок;
		
	КонецЕсли;	
	
	// ДЦ
	СтруктураСсылок.Вставить("ДЦ",         АСЦ_ПоискОбъектов.ПолучитьОбъектПоСоотвествию(Данные.ДЦ, Параметры.НастройкаДепартаменты, База, КэшДанных.Департамент));
	СтруктураСсылок.Вставить("БазаАстра",  АСЦ_ПоискОбъектов.ПолучитьОсновнуюБазу(СтруктураСсылок.ДЦ, КэшДанных.БазаАстра));
	
	// Контрагент
	ПараметрыКонтрагент = Новый Структура;
	ПараметрыКонтрагент.Вставить("ГУИД",         Данные.КонтрагентГУИД);
	ПараметрыКонтрагент.Вставить("Наименование", Данные.Контрагент);
	ПараметрыКонтрагент.Вставить("ИНН",          Данные.КонтрагентИНН);
	ПараметрыКонтрагент.Вставить("КПП",          Данные.КонтрагентКПП);
	ПараметрыКонтрагент.Вставить("ПаспортСерия", Данные.КонтрагентПаспортСерия);
	ПараметрыКонтрагент.Вставить("ПаспортНомер", Данные.КонтрагентПаспортНомер);
	ПараметрыКонтрагент.Вставить("ПаспортДата",  Данные.КонтрагентПаспортДата);
	
	Перезаписывать = НЕ ЗначениеЗаполнено(Данные.КонтрагентИНН);
	СтруктураСсылок.Вставить("Контрагент", АСЦ_ПоискОбъектов.ПолучитьКонтрагента(ПараметрыКонтрагент, Параметры, Параметры.НастройкаКонтрагенты, Перезаписывать));
	
	// Номенклатура
	НаименованиеНоменклатуры = ПолучитьНаименованиеНоменклатуры(Данные.Номенклатура, Данные.Пролонгация, Данные.ТипПродажи, Данные.TS_NEW);
	СтруктураСсылок.Вставить("Номенклатура", ПолучитьНоменклатуру(НаименованиеНоменклатуры, Данные.Номенклатура, КэшДанных.Номенклатура));
	
	// Страховая
	ТекстОшибки = "";
	ПараметрыСК = Новый Структура;
	ПараметрыСК.Вставить("ГУИД",         Данные.СтраховаяГУИД);
	ПараметрыСК.Вставить("Наименование", Данные.Страховая);
	ПараметрыСК.Вставить("ИНН",          Данные.СтраховаяИНН);
	ПараметрыСК.Вставить("КПП",          Данные.СтраховаяКПП);
	ПараметрыСК.Вставить("ЮрФизЛицо",    Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо);
	ПараметрыСК.Вставить("Родитель",     Справочники.Контрагенты.НайтиПоНаименованию("Страховые"));
	
	СтруктураСсылок.Вставить("СК", АСЦ_ПоискОбъектов.ПолучитьКонтрагента(ПараметрыСК, Параметры, Параметры.НастройкаСтраховые, Ложь));
	
	Если НЕ ЗначениеЗаполнено(Данные.Договор) Тогда
		Данные.Договор = "б/н";
	КонецЕсли;	
	
	// Договор контрагента
	ДанныеДоговора = Новый Структура;
	ДанныеДоговора.Вставить("Контрагент",     СтруктураСсылок.Контрагент);
	ДанныеДоговора.Вставить("Организация",    СтруктураСсылок.Организация);
	
	ДанныеДоговора.Вставить("Наименование",   СтрЗаменить(СокрЛП(Данные.Договор), Символы.НПП, " "));
	ДанныеДоговора.Вставить("ЦФО",            СтруктураСсылок.ДЦ);
	ДанныеДоговора.Вставить("ВидДоговораУХ",  Справочники.ВидыДоговоровКонтрагентовУХ.СПокупателем);
	ДанныеДоговора.Вставить("Дата",           Данные.ДоговорДата);
	ДанныеДоговора.Вставить("ДатаНачала",     Данные.ДатаНачала);
	ДанныеДоговора.Вставить("СрокДействия",   Данные.СрокДействия);
	ДанныеДоговора.Вставить("СтатьяДДС",      Справочники.СтатьиДвиженияДенежныхСредств.НайтиПоНаименованию("Поступление от реализации финансовых услуг"));
	
	Если Параметры.Свойство("ДоговорДопРеквизиты") Тогда
		ДополнительныеРеквизиты = Новый Соответствие;
		ДополнительныеРеквизиты.Вставить(Параметры.ДоговорДопРеквизиты.Страховая, СтруктураСсылок.СК);
		ДополнительныеРеквизиты.Вставить(Параметры.ДоговорДопРеквизиты.Банк,      АСЦ_ПоискОбъектов.НайтиБанк(Данные.БанкБИК));
		ДанныеДоговора.Вставить("ДополнительныеРеквизиты", ДополнительныеРеквизиты);
	КонецЕсли;	
	
	СтатьяДоходов = АСЦ_ПоискОбъектов.ПолучитьОбъектПоСоотвествию(НаименованиеНоменклатуры, Параметры.НастройкаСтатьиДоходов, База, КэшДанных.СтатьиДоходов);
	Если НЕ ЗначениеЗаполнено(СтатьяДоходов) Тогда
		
		Если Найти(НаименованиеНоменклатуры, ", пролонгация") > 0 Тогда
			СтатьяДоходов = Справочники.СтатьиДоходов.НайтиПоКоду("01713");  // Прочие, пролонгация
		Иначе
			СтатьяДоходов = Справочники.СтатьиДоходов.НайтиПоКоду("01715");  // Прочие
		КонецЕсли;	
		
	Иначе	
		ДанныеДоговора.Вставить("СтатьяДоходов",  СтатьяДоходов);
	КонецЕсли;	
	
	СтруктураСсылок.Вставить("СтатьяДоходов", СтатьяДоходов);
	СтруктураСсылок.Вставить("Договор", АСЦ_ПоискОбъектов.ПолучитьДоговор(ДанныеДоговора, Параметры.ПерезаполнятьДоговоры));
	
	// Договор СК
	Если ЗначениеЗаполнено(СтруктураСсылок.СК) Тогда
		
		ДанныеДоговораСК = Новый Структура;
		ДанныеДоговораСК.Вставить("Контрагент",    СтруктураСсылок.СК);
		ДанныеДоговораСК.Вставить("Организация",   СтруктураСсылок.Организация);
		ДанныеДоговораСК.Вставить("Наименование",  "Основной договор");
		ДанныеДоговораСК.Вставить("ВидДоговораУХ", Справочники.ВидыДоговоровКонтрагентовУХ.СКомитентом);
		ДанныеДоговораСК.Вставить("СтатьяДДС",     Справочники.СтатьиДвиженияДенежныхСредств.НайтиПоНаименованию("Выплаты по страховой деятельности"));
		
		СтруктураСсылок.Вставить("ДоговорСК", АСЦ_ПоискОбъектов.ПолучитьДоговор(ДанныеДоговораСК, Параметры.ПерезаполнятьДоговоры, Истина));
		
	КонецЕсли;
	
	Возврат СтруктураСсылок;
	
КонецФункции	

Функция ПолучитьНаименованиеНоменклатуры(Наименование, Пролонгация, ТипПродажи, TS_NEW)
	
	НаименованиеНоменклатуры = Наименование;
	Если Пролонгация = 1 Тогда
		Возврат НаименованиеНоменклатуры + ", пролонгация";
	КонецЕсли;
	
	Если ТипПродажи = "-" Тогда
		ТипПродажи = "";
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(ТипПродажи) Тогда
		
		Если TS_NEW = 1 Тогда
			ТипПродажи = "АМ розница";
		Иначе
			ТипПродажи = "АМ бу.";
		Конецесли;	
		
	КонецЕсли;
	
	НаименованиеНоменклатуры = НаименованиеНоменклатуры + ", " + ТипПродажи;
	
	Возврат НаименованиеНоменклатуры;
	
КонецФункции	

Функция ПолучитьНоменклатуру(Наименование, НомГруппа, Кэш)
	
	СпрСсылка = Кэш[Наименование];
	Если СпрСсылка <> Неопределено Тогда
		Возврат СпрСсылка;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Наименование", Наименование);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Спр.Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Спр
	|ГДЕ
	|	НЕ Спр.ЭтоГруппа
	|	И НЕ Спр.ПометкаУдаления
	|	И Спр.Наименование = &Наименование";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		СпрСсылка = Результат.Выгрузить()[0][0];
	Иначе
		
		СпрОбъект = Справочники.Номенклатура.СоздатьЭлемент();
		СпрОбъект.Наименование    =  Наименование;
		СпрОбъект.ВидНоменклатуры = Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Услуги");
		СпрОбъект.Услуга          = Истина;
		СпрОбъект.Родитель        = Справочники.Номенклатура.НайтиПоНаименованию("Страховки");
		СпрОбъект.НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.НайтиПоНаименованию(НомГруппа);
		СпрОбъект.СтавкаНДС       = Перечисления.СтавкиНДС.БезНДС;
		СпрОбъект.Записать();
		
		СпрСсылка = СпрОбъект.Ссылка;
		
	КонецЕсли;	
	
	Кэш.Вставить(Наименование, СпрСсылка);
	Возврат СпрСсылка;
	
КонецФункции

Функция ПолучитьОтчетПосредника(ДокументСсылка)
	
	Результат = Новый Структура("Ссылка");
	
	Если НЕ ЗначениеЗаполнено(ДокументСсылка) Тогда
		Возврат Результат;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Документ", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Док.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.АСЦ_ОтчетПосредникаУпр.Продажи КАК Док
	|ГДЕ
	|	Док.Документ = &Документ
	|	И Док.Ссылка.Проведен";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ЗаполнитьЗначенияСвойств(Результат, РезультатЗапроса.Выгрузить()[0]);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область РеализацииОбщие

Функция ПолучитьДанныеДокументаКВ(ДокументСсылка)
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Док.Организация КАК Организация,
	|	Док.Комитент КАК СК,
	|	Док.Контрагент КАК Контрагент,
	|	Док.Сумма КАК СуммаОплаты,
	|	Док.СуммаКВ КАК СуммаКВ,
	|	Док.ДЦ КАК ДЦ,
	|	Док.Статья КАК Статья,
	|	Док.ДоговорКонтрагента КАК Договор,
	|	Док.Номенклатура КАК Номенклатура
	|ИЗ
	|	Документ.АСЦ_ПлановоеНачислениеКВ КАК Док
	|ГДЕ
	|	Док.Ссылка = &Ссылка";
	
	Возврат Запрос.Выполнить().Выгрузить()[0];
	
КонецФункции	

Функция ИзменилисьСуммы(ДанныеДокумента, Данные, ДанныеСуммы, СтруктураСсылок)
	
	// Корректировку можно делать только по суммам
	Если Данные.СуммаОплаты <> ДанныеДокумента.СуммаОплаты Тогда
		Возврат Истина;
	КонецЕсли;	
	
	Если ДанныеСуммы.СуммаКВ <> ДанныеДокумента.СуммаКВ Тогда
		Возврат Истина;
	КонецЕсли;	
	
	Возврат Ложь;
	
КонецФункции	

Функция ИзменилисьРеквизиты(ДанныеДокумента, Данные, ДанныеСуммы, СтруктураСсылок)
	
	Если СтруктураСсылок.Договор <> ДанныеДокумента.Договор Тогда
		Возврат Истина;
	КонецЕсли;	
	
	Если СтруктураСсылок.Организация <> ДанныеДокумента.Организация Тогда
		Возврат Истина;
	КонецЕсли;	
	
	Если СтруктураСсылок.ДЦ <> ДанныеДокумента.ДЦ Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если СтруктураСсылок.Номенклатура <> ДанныеДокумента.Номенклатура Тогда
		Возврат Истина;
	КонецЕсли;	
	
	Возврат Ложь;
	
КонецФункции

Функция ИзменитьДЦ(ДокументСсылка, СтруктураСсылок, Параметры)
	
	НачатьТранзакцию();
	
	ДоговорОбъект = СтруктураСсылок.Договор.ПолучитьОбъект();
	СтараяБазаАстра = АСЦ_ПоискОбъектов.ПолучитьОсновнуюБазу(ДоговорОбъект.ОсновнойЦФО);
	
	ДоговорОбъект.ОсновнойЦФО = СтруктураСсылок.ДЦ;
	ДоговорОбъект.Записать();
	
	ДобавитьИзменениеОбъекта(ДокументСсылка, СтруктураСсылок.БазаАстра);
	
	// Для удаления в старой базе
	Если СтараяБазаАстра <> СтруктураСсылок.БазаАстра Тогда
		ДобавитьИзменениеОбъекта(ДокументСсылка, СтараяБазаАстра);
	КонецЕсли;
	
	ЗафиксироватьТранзакцию();
	
КонецФункции	

#КонецОбласти

#Область Реализации

Процедура ЗагрузитьРеализации(Параметры, АдресРезультата = Неопределено, Знач ТаблицаДанных = Неопределено) Экспорт
	
	База        = Параметры.База;
	ДатаНач     = Параметры.ДатаНач;
	Если НЕ ЗначениеЗаполнено(ДатаНач) Тогда
		ДатаНач = '2018-01-01';
	КонецЕсли;	
	ДатаКон     = Параметры.ДатаКон;
	ПерезаполнятьДокументы = Параметры.ПерезаполнятьДокументы;
	ПерезаполнятьДоговоры  = Параметры.ПерезаполнятьДоговоры;
	ВыводитьСообщения      = Параметры.ВыводитьСообщения;
	ОтложенноеПроведение   = Параметры.ОтложенноеПроведение;
	
	ТипБазы   = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(База, "ТипБД"); 
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("База",                   База);
	ДопПараметры.Вставить("КонтрагентРодительФЛ",   Справочники.Контрагенты.НайтиПоНаименованию("1 Физические лица"));
	ДопПараметры.Вставить("КонтрагентРодительЮЛ",   Справочники.Контрагенты.НайтиПоНаименованию("1 Юридические лица"));
	ДопПараметры.Вставить("НастройкаДепартаменты",  Справочники.СоответствиеВнешнимИБ.НайтиПоНаименованию("DEPARTMENTS <-> Организации",,, ТипБазы));
	ДопПараметры.Вставить("НастройкаСтатьиДоходов", Справочники.СоответствиеВнешнимИБ.НайтиПоНаименованию("Номенклатура <-> Статьи доходов",,, ТипБазы));
	ДопПараметры.Вставить("НастройкаКонтрагенты",   Справочники.СоответствиеВнешнимИБ.НайтиПоНаименованию("Контрагенты <-> Контрагенты",,, ТипБазы));
	ДопПараметры.Вставить("НастройкаСтраховые",     Справочники.СоответствиеВнешнимИБ.НайтиПоНаименованию("Страховые <-> Контрагенты",,, ТипБазы));
	ДопПараметры.Вставить("ПерезаполнятьДокументы", ПерезаполнятьДокументы);
	ДопПараметры.Вставить("ПерезаполнятьДоговоры",  ПерезаполнятьДоговоры);
	ДопПараметры.Вставить("ВыводитьСообщения",      ВыводитьСообщения);
	ДопПараметры.Вставить("ОтложенноеПроведение",   ОтложенноеПроведение);
	
	ДопПараметры.Вставить("СвойствоID",           ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ID_Unicus"));
	
	ДоговорДопРеквизиты = Новый Структура;
	ДоговорДопРеквизиты.Вставить("Страховая", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "Страховая"));
	ДоговорДопРеквизиты.Вставить("Банк",      ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "Банк"));
	
	ДопПараметры.Вставить("ДоговорДопРеквизиты", ДоговорДопРеквизиты);
	ДлительныеОперации.СообщитьПрогресс(0, "Получение данных из Unicus");
	
	Если ТаблицаДанных = Неопределено Тогда
		ТаблицаДанных = ПолучитьТаблицуДанныхРеализации(База, ДатаНач, ДатаКон, Параметры);
		// Заранее подготовленные данные, для отладки создания документов
		//ТаблицаДанных = ПолучитьТаблицуДанныхРеализацииТест();
	КонецЕсли;	
	
	КэшДанных = Новый Структура;
	КэшДанных.Вставить("Организация",  Новый Соответствие);
	КэшДанных.Вставить("Департамент",  Новый Соответствие);
	КэшДанных.Вставить("Страховая",    Новый Соответствие);
	КэшДанных.Вставить("Номенклатура", Новый Соответствие);
	КэшДанных.Вставить("СведенияОНоменклатуре", Новый Соответствие);
	КэшДанных.Вставить("СтатьиДоходов", Новый Соответствие);
	КэшДанных.Вставить("ДатыЗапрета",   Новый Соответствие);
	КэшДанных.Вставить("БазаАстра",     Новый Соответствие);
	
	КэшСообщений = Новый Структура;
	КэшСообщений.Вставить("Организация",   Новый Соответствие);
	КэшСообщений.Вставить("Страховая",     Новый Соответствие);
	КэшСообщений.Вставить("Департамент",   Новый Соответствие);
	КэшСообщений.Вставить("Номенклатура",  Новый Соответствие);
	КэшСообщений.Вставить("СтатьяДоходов", Новый Соответствие);
	
	Всего   = ТаблицаДанных.Количество();
	Счетчик = 0;
	
	Для каждого СтрокаТЗ из ТаблицаДанных Цикл
		
		СтруктураСсылок = ПолучитьСтруктуруСсылок(СтрокаТЗ, ДопПараметры, База, КэшДанных, КэшСообщений);
		Если НЕ ЗначениеЗаполнено(СтруктураСсылок.Организация) Тогда
			Продолжить;
		КонецЕсли;	
		
		ДокРТУ = СоздатьДокумент(СтрокаТЗ, ДопПараметры, СтруктураСсылок, КэшДанных);
		
		Счетчик = Счетчик + 1;
		ПроцентВыполнения = Окр(100 * Счетчик / Всего, 2);
		ДлительныеОперации.СообщитьПрогресс(ПроцентВыполнения, "Загружено " + Счетчик + " из " + Всего);
		
	КонецЦикла;	
	
КонецПроцедуры	

Функция СоздатьДокумент(Данные, Параметры, СтруктураСсылок, КэшДанных) Экспорт
	
	ДокументКВ = НайтиДокументПоДоговору(СтруктураСсылок.Договор);
	
	Если ЗначениеЗаполнено(ДокументКВ) Тогда
		
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументКВ, "Дата");
		Данные.Дата = Реквизиты.Дата;
		
	КонецЕсли;	
	
	// Если уже сформирован отчет посредника, то делаем корректировку
	СтруктураОтчетаПосредника = ПолучитьОтчетПосредника(ДокументКВ);
	Если ЗначениеЗаполнено(СтруктураОтчетаПосредника.Ссылка) Тогда
		Действие = Новый Структура;
		Действие.Вставить("Имя", "Корректировка");
	Иначе	
		Действие = ДействиеПоДатеЗапрета(Данные.Дата, Данные.ДатаИзменения, СтруктураСсылок.ДЦ, КэшДанных.ДатыЗапрета);
	КонецЕсли;	
	
	Если Действие.Имя = "Редактирование" Тогда	
		
		// В открытом периоде сторно не делаем,
		// просто редактируем документ
		ДокументСсылка = ЗаполнитьДокументНачислениеКВ(ДокументКВ, Данные, Параметры, СтруктураСсылок);
		
	ИначеЕсли Действие.Имя = "Корректировка"
		И ЗначениеЗаполнено(ДокументКВ) Тогда
		
		ДокументСсылка = ЗаполнитьДокументНачислениеКВ(ДокументКВ, Данные, Параметры, СтруктураСсылок, ДокументКВ);
		
	//Документы закрытого периода вводим 1 числом текущего месяца
	Иначе
		
		Данные.Дата = Действие.ДатаЗапрета + 1;
		ДокументСсылка = ЗаполнитьДокументНачислениеКВ(ДокументКВ, Данные, Параметры, СтруктураСсылок);
		
	КонецЕсли;
	
	Возврат ДокументСсылка;
	
КонецФункции

Функция ЗаполнитьДокументНачислениеКВ(ДокументСсылка, Данные, Параметры, СтруктураСсылок, Основание = Неопределено) Экспорт
	
	ДанныеСуммы = ПолучитьСуммы(Данные);
	
	Если ЗначениеЗаполнено(ДокументСсылка) Тогда
		
		ДанныеДокумента = ПолучитьДанныеДокументаКВ(ДокументСсылка);
		
		// Изменилось ДЦ,
		// тк ДЦ в договоре, то меняем сразу и помечаем к выгрузке
		Если ДанныеДокумента.ДЦ <> СтруктураСсылок.ДЦ Тогда
			ИзменитьДЦ(ДокументСсылка, СтруктураСсылок, Параметры);	
		КонецЕсли;
		
		Если ИзменилисьСуммы(ДанныеДокумента, Данные, ДанныеСуммы, СтруктураСсылок)
			ИЛИ ИзменилисьРеквизиты(ДанныеДокумента, Данные, ДанныеСуммы, СтруктураСсылок) Тогда
			
			// Редактирование документа
			ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
			
			// документ не изменился
		Иначе
			
			Если Параметры.ПерезаполнятьДокументы Тогда
				ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
			Иначе	
				Возврат ДокументСсылка;
			КонецЕсли;	
			
		КонецЕсли;	
		
	Иначе
		
		// Новый документ
		ДокументОбъект = Документы.АСЦ_ПлановоеНачислениеКВ.СоздатьДокумент();
		
	КонецЕсли;	
	
	Если Основание <> Неопределено Тогда
		ДокументОбъект.Основание = Основание;
	КонецЕсли;	
	
	ДокументОбъект.Дата        = Данные.Дата;
	Если Данные.Номенклатура = "Диагностическая карта" Тогда
		ДокументОбъект.ВидОперации = Справочники.АСЦ_ВидыОперацийПлановоеНачислениеКВ.ДиагностическаяКарта;
	Иначе	
		ДокументОбъект.ВидОперации = Справочники.АСЦ_ВидыОперацийПлановоеНачислениеКВ.ОплатаВБанке;
	КонецЕсли;	
	ДокументОбъект.Организация = СтруктураСсылок.Организация;
	ДокументОбъект.ДЦ          = СтруктураСсылок.ДЦ;
	ДокументОбъект.Контрагент  = СтруктураСсылок.Контрагент;
	ДокументОбъект.ДоговорКонтрагента = СтруктураСсылок.Договор;
	
	ДокументОбъект.Комитент = СтруктураСсылок.СК; 
	
	ДокументОбъект.VIN = Данные.VIN;
	ДокументОбъект.Номенклатура = СтруктураСсылок.Номенклатура;
	ДокументОбъект.Статья       = СтруктураСсылок.СтатьяДоходов;
	
	ДокументОбъект.ВалютаДокумента = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	ДокументОбъект.Сумма           = Данные.СуммаОплаты;
	
	Если ДокументОбъект.ВидОперации.ФиксированнаяСебестоимость Тогда
		
		ТаблицаЦен = Ценообразование.ПолучитьТаблицуЦенНоменклатуры(ДокументОбъект.Номенклатура, Константы.ТипЦенПлановойСебестоимостиНоменклатуры.Получить(), ДокументОбъект.Дата);
		Себестоимость = 0;
		Если ТаблицаЦен.Количество() > 0 Тогда
			Себестоимость = ТаблицаЦен[0].Цена;
		КонецЕсли;	
		ДокументОбъект.СуммаКВ = ДокументОбъект.Сумма - Себестоимость;
		ДокументОбъект.ПроцентКВ = 0;
		
	Иначе	
		ДокументОбъект.ПроцентКВ = Данные.KV;
		ДокументОбъект.СуммаКВ   = ДанныеСуммы.СуммаКВ;
	КонецЕсли;	
	
	НачатьТранзакцию();
	ЗаписатьДокумент(ДокументОбъект, РежимЗаписиДокумента.Проведение, Параметры.ВыводитьСообщения, Параметры.ОтложенноеПроведение);		
	ЗафиксироватьТранзакцию();
	
	СохранитьДатуПоследнегоДокумента(Данные.ДатаИзменения);
	
КонецФункции	

#КонецОбласти
