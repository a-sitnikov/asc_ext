#Область ОписаниеОбработки

Функция СведенияОВнешнейОбработке() Экспорт

	//ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("2.1.3.1");
	//ПараметрыРегистрации.БезопасныйРежим = Ложь;
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("2.2.2.1");
	
	// HKEY_LOCAL_MACHINE\SOFTWARE\Classes\ADODB.Connection
	Разрешение = РаботаВБезопасномРежиме.РазрешениеНаСозданиеCOMКласса("ADODB.Connection", "{00000514-0000-0010-8000-00AA006D2EA4}");
	ПараметрыРегистрации.Разрешения.Добавить(Разрешение);
	
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительнаяОбработка();
	ПараметрыРегистрации.Версия = Метаданные().Комментарий;
	ПараметрыРегистрации.БезопасныйРежим = Истина;
	ПараметрыРегистрации.Информация = Метаданные().Представление();
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Представление = Метаданные().Представление() + " - Открыть форму";
	НоваяКоманда.Идентификатор = Метаданные().Имя + "Форма";
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	НоваяКоманда.ПоказыватьОповещение = Ложь;
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Представление = "Загрузить отчеты посредника Юникус";
	НоваяКоманда.Идентификатор = "ЗагрузитьОтчеты";
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
	НоваяКоманда.ПоказыватьОповещение = Ложь;

	Возврат ПараметрыРегистрации;

КонецФункции

Процедура ВыполнитьКоманду(ИдентификаторКоманды, ПараметрыКоманды = Неопределено) Экспорт 
	
	//Если АСЦ_Обмены.РаботаСВнешнимиРесурсамиЗаблокирована() Тогда
	//	Сообщение = Новый СообщениеПользователю;
	//	Сообщение.Текст = 
	//		"Работа с внешними ресурсами заблокирована.
	//		|Обмен можно проводить только через форму";
	//	Сообщение.Сообщить();
	//	Возврат;
	//КонецЕсли;	
	
	Параметры = Новый Структура;
	
	База = ХранилищеОбщихНастроек.Загрузить("ЗагрузитьДокументыИзUnicus", "База",, "ЗагрузкаДокументовИзUnicus");
	Если НЕ ЗначениеЗаполнено(База) Тогда
		База = Справочники.ВнешниеИнформационныеБазы.НайтиПоНаименованию("Юникус");
	КонецЕсли;
	
	// начало пред. месяца
	ДатаНач = НачалоМесяца(НачалоМесяца(ТекущаяДата()) - 1);
	ДатаНач = Макс(ДатаНач, '2018-01-01');
	
	Параметры.Вставить("База",        База);
	Параметры.Вставить("ДатаНач",     ДатаНач);
	Параметры.Вставить("ДатаКон",     КонецДня(ТекущаяДата()));
	Параметры.Вставить("ПерезаполнятьДокументы",   Ложь);
	Параметры.Вставить("ПерезаполнятьДоговоры",    Ложь);
	Параметры.Вставить("ВыводитьСообщения",        Ложь);
	Параметры.Вставить("ОтложенноеПроведение",     Истина);
	
	Если ИдентификаторКоманды = "ЗагрузитьОтчеты" Тогда
		ЗагрузитьОтчеты(Параметры);
	КонецЕсли;	
	
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеФункции

Функция ЗаписатьДокумент(ДокОбъект, Режим, Сообщать = Истина, ОтложенноеПроведение = Ложь)
	
	Результат = Истина;	
	
	Если ДокОбъект = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;	
	
	// тк некуда записывать пропущенные документы, то будем просто останавливать обработку
	Если ОтложенноеПроведение Тогда
		
		НачатьТранзакцию();
		
		ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
		
		Запись = РегистрыСведений.ОтложенныеДвиженияДокументов.СоздатьМенеджерЗаписи();
		Запись.Документ      = ДокОбъект.Ссылка;
		Запись.УзелОбмена    = ПланыОбмена.Полный.ЭтотУзел();
		Запись.ДатаДокумента = ДокОбъект.Дата;
		Запись.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Иначе	
		ДокОбъект.Записать(Режим);
	КонецЕсли;	
	
	Если Сообщать Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Строка(ДокОбъект);
		Сообщение.КлючДанных = ДокОбъект.Ссылка;
		Сообщение.Сообщить();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции	
	
Функция ЗначенияОтличаются(Структруа1, Структура2)
	
	Для каждого КлючИЗначение Из Структруа1 Цикл
		
		Если КлючИЗначение.Значение <> Структура2[КлючИЗначение.Ключ] Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;	
	
	Возврат Ложь;
	
КонецФункции	

Функция НайтиДокументНачислениеКВ(ИД) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("ВнешнийИД", ИД);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Рег.Ссылка КАК Ссылка,
	|	Рег.Дата КАК Дата
	|ИЗ
	|	Документ.АСЦ_ПлановоеНачислениеКВ КАК Рег
	|ГДЕ
	|	Рег.ВнешнийИД = &ВнешнийИД
	|	И НЕ Рег.ЭтоКорректировка
	|	И НЕ Рег.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Возврат Результат.Выгрузить()[0][0];
	Иначе	
		Возврат Неопределено;
	КонецЕсли;	
		
КонецФункции	

#КонецОбласти

#Область ПолучениеДанных

Функция ПолучитьТекстЗапроса(ДатаНач, ДатаКон, Параметры) Экспорт
	
	ТекстЗапроса =
	"SELECT 
	|    TO_CHAR(head.act_id) as act_id,    
	|    head.НомерДок,    
	|    head.ДатаДок,    
	|    head.ДатаНачала,    
	//|    head.ДатаОкончания,    
	|    head.СК,  
	|    head.СК_Договор,  
	|    tab.SK_INN as СК_ИНН,
	|    tab.SK_KPP as СК_КПП,
	|    TO_NUMBER(tab.COL2) as НомерСтроки,
	|	 tab.AGENT_INN as ОрганизацияИНН,
	|	 tab.AGENT_KPP as ОрганизацияКПП,
	|    tab.COL3 as Контрагент,
	|    tab.COL10 as КонтрагентИНН,
	|    tab.COL11 as КонтрагентКПП,
	|    REPLACE(tab.COL4 || tab.COL5, ' ', '') as НомерПолиса,
	|    TO_NUMBER(tab.COL6, '999999999999.99') as СтраховаяПремия,
	|    tab.COL7 as СуммаКВ,
	|    tab.COL8 as ПроцентКВ,
	|    tab.COL9 as СуммаКПеречислению,
  	|    TO_CHAR(tab.PAY_ID) as ID
	|FROM
	|	(SELECT 
	|   		ACT_ID, 
	|		    COL2 as НомерДок, 
	//|   		NVL(REGEXP_SUBSTR(COL2, '[0-9]{2}\.[0-9]{2}\.[0-9]{4}'), '01.01.0001') as  ДатаОкончания,
	|			COL3 as ДатаНачала,
	|		    COL4 as СК,
	|		    COL6 as СК_Договор,
	|			DATE_SIGN as ДатаДок
	|		FROM 
	|			unicusweb.v_asc_act
	|		WHERE
	|			COL1 is NOT NULL
	|			AND DATE_SIGN BETWEEN &ДатаНач AND &ДатаКон
	|			AND ACT_STATUS_NAME IN ('Принят СК', 'Оплачен')
	|	) head
	|	INNER JOIN unicusweb.v_asc_act tab 
	|		ON tab.act_id = head.act_id
	|WHERE
	|	tab.COL1 IS NULL
	|	AND tab.PAY_DATE IS NOT NULL
	|	AND tab.PAY_DATE >= &Дата010118";
	
	Если Параметры.Свойство("ОтборОрганизация") Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
		|	AND tab.AGENT_INN = '" + ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ОтборОрганизация, "ИНН") + "'";
		
		КПП = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ОтборОрганизация, "КПП");
		Если ЗначениеЗаполнено(КПП) Тогда
			
			ТекстЗапроса = ТекстЗапроса + "
			|	AND tab.AGENT_KPP = '" + КПП + "'";
			
		КонецЕсли;	
		
	КонецЕсли;	
		
	Если Параметры.Свойство("ОтборID") Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
		|	AND head.act_id = " + Формат(Параметры.ОтборID, "ЧГ=0") + "";
		
	КонецЕсли;	
	
	Если Параметры.Свойство("ИмяТаблицы") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "unicusweb.v_asc_act", Параметры.ИмяТаблицы);
	КонецЕсли;	
	
	ТекстЗапроса = ТекстЗапроса + "
	|ORDER BY
	|	act_id, НомерСтроки";
	
	// Отладочные отборы
	//Доп отбор для тестирования
	// например AND rownum <= 10  - выбираем только первые 10 записей
	Если Параметры.Свойство("Тест_КоличествоСтрок") Тогда
		
		ТекстЗапроса =  
		"SELECT
		| *
		|FROM (
		|" + ТекстЗапроса + "
		|)
		|WHERE
		|	rownum <= " + Формат(Параметры.Тест_КоличествоСтрок, "ЧГ=0");
		
	КонецЕсли;	
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Дата010118", ИнтеграцияСВнешнимиСистемамиУХ.АСЦ1_асцПолучитьТекстПараметра('2018-01-01', "Oracle"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДатаНач", ИнтеграцияСВнешнимиСистемамиУХ.АСЦ1_асцПолучитьТекстПараметра(ДатаНач, "Oracle"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДатаКон", ИнтеграцияСВнешнимиСистемамиУХ.АСЦ1_асцПолучитьТекстПараметра(ДатаКон, "Oracle"));
	
	Возврат ТекстЗапроса;
КонецФункции	

Функция ПолучитьТаблицуДанных(База, ДатаНач, ДатаКон, Параметры)
	
	ТекстЗапроса = ПолучитьТекстЗапроса(ДатаНач, ДатаКон, Параметры);
	СтрокаСоединения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(База, "СтрокаПодключения");
	
	Соединение = Новый COMОбъект("ADODB.Connection");
	Соединение.Open(СтрокаСоединения);
	
	Возврат АСЦ_ОбщегоНазначения.ВыполнитьЗапросADO(Соединение, ТекстЗапроса);
	
Конецфункции	

Функция ПолучитьТаблицуДанныхТест(Знач Макет = Неопределено) Экспорт
	
	// Для тестирования разных ситуаций подготовим таблицу вручную
	Если Макет = Неопределено Тогда
		Макет = ПолучитьМакет("ТестовыеДанные");
	КонецЕсли;	
	
	ТаблицаЗначений = Новый ТаблицаЗначений;
	Для Счетчик = 2 По Макет.ШиринаТаблицы Цикл
		
		Тип = Макет.Область(1, Счетчик).Текст;
		Имя = Макет.Область(2, Счетчик).Текст;
		Если Тип = "Дата" Тогда
			Тип = Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя));
		ИначеЕсли Тип = "Число" Тогда	
			Тип = Новый ОписаниеТипов("Число");
		Иначе
			Тип = Неопределено;
		КонецЕсли;
		
		ТаблицаЗначений.Колонки.Добавить(Имя, Тип);
		
	КонецЦикла;	
	
	Для СчетчикСтрок = 3 По Макет.ВысотаТаблицы Цикл
		
		// В первой колонке пометка
		Пометка = Макет.Область(СчетчикСтрок, 1).Текст;
		Если НЕ ЗначениеЗаполнено(Пометка) Тогда
			Продолжить;
		КонецЕсли;	
		
		НоваяСтрока = ТаблицаЗначений.Добавить();
		
		Для каждого Колонка из ТаблицаЗначений.Колонки Цикл
			
			СчетчикКолонок = ТаблицаЗначений.Колонки.Индекс(Колонка) + 2;
			Значение = Макет.Область(СчетчикСтрок, СчетчикКолонок).Текст;
			
			Если ТипЗнч(НоваяСтрока[Колонка.Имя]) = Тип("Дата") Тогда
				НоваяСтрока[Колонка.Имя] = Дата(Значение);
			ИначеЕсли ТипЗнч(НоваяСтрока[Колонка.Имя]) = Тип("Число") Тогда
				НоваяСтрока[Колонка.Имя] = Число(Значение);
			Иначе
				НоваяСтрока[Колонка.Имя] = Значение;
			КонецЕсли;	
			
		КонецЦикла;
		
	КонецЦикла;	
	
	Возврат ТаблицаЗначений;
	
Конецфункции	

#КонецОбласти

#Область Отчеты

Процедура ЗагрузитьОтчеты(Параметры, АдресРезультата = Неопределено, Знач ТаблицаДанных = Неопределено) Экспорт
	
	База        = Параметры.База;
	ДатаНач     = Параметры.ДатаНач;
	ДатаКон     = Параметры.ДатаКон;
	ПерезаполнятьДокументы = Параметры.ПерезаполнятьДокументы;
	ОтложенноеПроведение   = Параметры.ОтложенноеПроведение;
	ВыводитьСообщения      = Параметры.ВыводитьСообщения;
	
	//Тест_КоличествоСтрок = 0;
	//Параметры.Свойство("Тест_КоличествоСтрок", Тест_КоличествоСтрок);
	//
	//ОтборОрганизация = 0;
	//Параметры.Свойство("ОтборОрганизация", ОтборОрганизация);
	//
	//ОтборID = 0;
	//Параметры.Свойство("ОтборID", ОтборID);
	
	Тест_НеСоздаватьСвязныеДокументы = Ложь;
	Параметры.Свойство("Тест_НеСоздаватьСвязныеДокументы", Тест_НеСоздаватьСвязныеДокументы);
	
	ТипБазы   = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(База, "ТипБД"); 
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("КонтрагентРодительФЛ",   Справочники.Контрагенты.НайтиПоНаименованию("1 Физические лица"));
	ДопПараметры.Вставить("КонтрагентРодительЮЛ",   Справочники.Контрагенты.НайтиПоНаименованию("1 Юридические лица"));
	ДопПараметры.Вставить("СвойствоID",             ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ID_Unicus"));
	ДопПараметры.Вставить("ПерезаполнятьДокументы", ПерезаполнятьДокументы);
	ДопПараметры.Вставить("ОтложенноеПроведение",   ОтложенноеПроведение);
	
	ДлительныеОперации.СообщитьПрогресс(0, "Получение данных из Unicus");
	
	Если ТаблицаДанных = Неопределено Тогда
		ТаблицаДанных = ПолучитьТаблицуДанных(База, ДатаНач, ДатаКон, Параметры);
	КонецЕсли;	
	//ТаблицаДанных = ПолучитьТаблицуДанныхТест();
	
	КэшДанных = Новый Структура;
	КэшДанных.Вставить("Организация",  Новый Соответствие);
	КэшДанных.Вставить("Номенклатура", Новый Соответствие);
	
	КэшСообщений = Новый Структура;
	КэшСообщений.Вставить("Организация",  Новый Соответствие);
	КэшСообщений.Вставить("Департамент",  Новый Соответствие);
	КэшСообщений.Вставить("Номенклатура", Новый Соответствие);
	
	Всего   = ТаблицаДанных.Количество();
	Счетчик = 0;
	
	ПредЗначения = Новый Структура("act_id");
	ДокОбъект    = Неопределено;
	
	Для каждого СтрокаТЗ из ТаблицаДанных Цикл
		
		Если ЗначенияОтличаются(ПредЗначения, СтрокаТЗ) Тогда
			
			ЗаполнитьЗначенияСвойств(ПредЗначения, СтрокаТЗ);
			
			Если ДокОбъект <> Неопределено Тогда
		
				ДокОбъект.Проведен = Истина;
				ЗаписатьДокумент(ДокОбъект, РежимЗаписиДокумента.Запись, ВыводитьСообщения, ОтложенноеПроведение);
				
			КонецЕсли;	
			
			ДокОбъект = СоздатьДокументОтчетПосредника(СтрокаТЗ, ДопПараметры, База, КэшДанных, КэшСообщений);
			
			Если Ложь Тогда
				ДокОбъект = Документы.АСЦ_ОтчетПосредникаУпр.СоздатьДокумент();
			КонецЕсли;	
			
			// Неопределено когда документ не перезаполняем
			Если ДокОбъект <> Неопределено Тогда
				
				ДокОбъект.ВидОперации  = Справочники.АСЦ_ВидыОперацийПлановоеНачислениеКВ.Страховки;
				ДокОбъект.ВнешнийИД    = СтрокаТЗ.act_id;
				ДокОбъект.ВнешнийНомер = СтрокаТЗ.НомерДок;
				ДокОбъект.Дата         = СтрокаТЗ.ДатаДок;
				ДокОбъект.Организация  = АСЦ_ПоискОбъектов.ПолучитьОрганизациюПоИНН(СтрокаТЗ.ОрганизацияИНН, СтрокаТЗ.ОрганизацияКПП, КэшДанных.Организация);
				
				ТекстОшибки = "";
				ДокОбъект.Контрагент  = АСЦ_ПоискОбъектов.НайтиКонтрагента("", СтрокаТЗ.СК_ИНН, СтрокаТЗ.СК_КПП, ТекстОшибки);
				Если НЕ ЗначениеЗаполнено(ДокОбъект.Контрагент) Тогда
					ТекстОшибки = "Не найдена СК: " + СтрокаТЗ.СК + ", ИНН: " + СтрокаТЗ.СК_ИНН + ", КПП: " + СтрокаТЗ.СК_КПП;
					ЗаписьЖурналаРегистрации("Загрузка реализаций из Unicus",
						УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
				КонецЕсли;	
				
			КонецЕсли;
			
		КонецЕсли;	
		
		Счетчик = Счетчик + 1;
		ПроцентВыполнения = Окр(100 * Счетчик / Всего, 2);
		ДлительныеОперации.СообщитьПрогресс(ПроцентВыполнения, "Обработано: " + Счетчик + " из " + Всего + " строк");
		
		Если ДокОбъект <> Неопределено Тогда
			
			// Тут заполнение таб. части
			НоваяСтрока = ДокОбъект.Продажи.Добавить();
			НоваяСтрока.Документ = НайтиДокументНачислениеКВ(СтрокаТЗ.ID);
			НоваяСтрока.ВнешнийИД = Формат(СтрокаТЗ.ID, "ЧГ=0");
			НоваяСтрока.ВнешнийНомерДоговора = СтрокаТЗ.НомерПолиса;
			
			НоваяСтрока.Сумма        = СтрокаТЗ.СтраховаяПремия;
			НоваяСтрока.ПроцентКВ    = СтрокаТЗ.ПроцентКВ;
			НоваяСтрока.СуммаКВ      = СтрокаТЗ.СуммаКВ;
			
			Если ЗначениеЗаполнено(НоваяСтрока.Документ) Тогда
				
				РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НоваяСтрока.Документ, "ДЦ, Статья, Сумма, ПроцентКВ, СуммаКВ, Контрагент, VIN");
				НоваяСтрока.СуммаПлан     = РеквизитыДокумента.Сумма;
				НоваяСтрока.ПроцентКВПлан = РеквизитыДокумента.ПроцентКВ;
				НоваяСтрока.СуммаКВПлан   = РеквизитыДокумента.СуммаКВ;
				
				НоваяСтрока.ДЦ            = РеквизитыДокумента.ДЦ;
				НоваяСтрока.Статья        = РеквизитыДокумента.Статья;
				НоваяСтрока.Контрагент    = РеквизитыДокумента.Контрагент;
				НоваяСтрока.VIN           = РеквизитыДокумента.VIN;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;	
	
	Если ДокОбъект <> Неопределено Тогда
		
		ДокОбъект.Проведен = Истина;
		ЗаписатьДокумент(ДокОбъект, РежимЗаписиДокумента.Запись, ВыводитьСообщения, ОтложенноеПроведение);
		
	КонецЕсли;	
		
КонецПроцедуры	

Функция СоздатьДокументОтчетПосредника(Данные, Параметры, База, КэшДанных, КэшСообщений)
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("ВнешнийИД", Данные.act_id); 
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.АСЦ_ОтчетПосредникаУпр КАК Док
	|ГДЕ
	|	Док.ВнешнийИД = &ВнешнийИД";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		
		ДокументСсылка = Результат.Выгрузить()[0][0];
		
		Если Параметры.ПерезаполнятьДокументы = Истина Тогда
			ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
			ДокументОбъект.Продажи.Очистить();
			ДокументОбъект.Проведен = Ложь;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
	Иначе
		ДокументОбъект = Документы.АСЦ_ОтчетПосредникаУпр.СоздатьДокумент();
	КонецЕсли;
	
	Возврат ДокументОбъект;
	
КонецФункции	

#КонецОбласти

