#Область ОписаниеОбработки

Функция СведенияОВнешнейОбработке() Экспорт

	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("2.1.3.1");
	
	// Запуск через фоновое не работает в режиме разрешений
	// Каталог не обнаружен 'e1cib\tempstorage\dee284a6-d234-4482-b97c-3ab694a1d1dd'
	//ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("2.2.2.1");
	//
	//// HKEY_LOCAL_MACHINE\SOFTWARE\Classes\ADODB.Connection
	//Разрешение = РаботаВБезопасномРежиме.РазрешениеНаСозданиеCOMКласса("ADODB.Connection", "{00000514-0000-0010-8000-00AA006D2EA4}");
	//ПараметрыРегистрации.Разрешения.Добавить(Разрешение);
	
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительнаяОбработка();
	ПараметрыРегистрации.Версия = Метаданные().Комментарий;
	ПараметрыРегистрации.БезопасныйРежим = Истина;
	ПараметрыРегистрации.Информация = Метаданные().Представление();
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Представление = Метаданные().Представление() + " - Открыть форму";
	НоваяКоманда.Идентификатор = Метаданные().Имя + "Форма";
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	НоваяКоманда.ПоказыватьОповещение = Ложь;
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Представление = "Загрузить отчеты посредника Юникус";
	НоваяКоманда.Идентификатор = "ЗагрузитьОтчеты";
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
	НоваяКоманда.ПоказыватьОповещение = Ложь;

	Возврат ПараметрыРегистрации;

КонецФункции

Процедура ВыполнитьКоманду(ИдентификаторКоманды, ПараметрыКоманды = Неопределено) Экспорт 
	
	Параметры = Новый Структура;
	
	База = ХранилищеОбщихНастроек.Загрузить("ЗагрузитьДокументыИзUnicus", "База",, "ЗагрузкаДокументовИзUnicus");
	Если НЕ ЗначениеЗаполнено(База) Тогда
		База = Справочники.ВнешниеИнформационныеБазы.НайтиПоНаименованию("Юникус");
	КонецЕсли;
	
	Параметры.Вставить("База",        База);
	Параметры.Вставить("ДатаНач",     ХранилищеОбщихНастроек.Загрузить("ЗагрузкаДокументовИзUnicus", "ДатаПоследнегоДокумента",, "ЗагрузкаДокументовИзUnicus"));
	Параметры.Вставить("ДатаКон",     '2100-01-01');
	Параметры.Вставить("Организация", Неопределено);
	Параметры.Вставить("ПерезаполнятьДокументы",   Ложь);
	Параметры.Вставить("ПерезаполнятьДоговоры",    Ложь);
	Параметры.Вставить("ВыводитьСообщения",        Ложь);
	Параметры.Вставить("ОтложенноеПроведение",     Истина);
	
	Если ИдентификаторКоманды = "ЗагрузитьОтчеты" Тогда
		ЗагрузитьОтчеты(Параметры);
	КонецЕсли;	
	
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеФункции

Функция ЗаписатьДокумент(ДокОбъект, Режим, Сообщать = Истина, ОтложенноеПроведение = Ложь)
	
	Результат = Истина;	
	
	Если ДокОбъект = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;	
	
	// тк некуда записывать пропущенные документы, то будем просто останавливать обработку
	Если ОтложенноеПроведение Тогда
		
		НачатьТранзакцию();
		
		ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
		
		Запись = РегистрыСведений.ОтложенныеДвиженияДокументов.СоздатьМенеджерЗаписи();
		Запись.Документ      = ДокОбъект.Ссылка;
		Запись.УзелОбмена    = ПланыОбмена.Полный.ЭтотУзел();
		Запись.ДатаДокумента = ДокОбъект.Дата;
		Запись.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Иначе	
		ДокОбъект.Записать(Режим);
	КонецЕсли;	
	
	Если Сообщать Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Строка(ДокОбъект);
		Сообщение.КлючДанных = ДокОбъект.Ссылка;
		Сообщение.Сообщить();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции	
	

Функция ЗначенияОтличаются(Структруа1, Структура2)
	
	Для каждого КлючИЗначение Из Структруа1 Цикл
		
		Если КлючИЗначение.Значение <> Структура2[КлючИЗначение.Ключ] Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;	
	
	Возврат Ложь;
	
КонецФункции	

#КонецОбласти

#Область Отчеты

Процедура ЗагрузитьОтчеты(Параметры, АдресРезультата = Неопределено) Экспорт
	
	База        = Параметры.База;
	ДатаНач     = Параметры.ДатаНач;
	ДатаКон     = Параметры.ДатаКон;
	ПерезаполнятьДокументы = Параметры.ПерезаполнятьДокументы;
	ОтложенноеПроведение   = Параметры.ОтложенноеПроведение;
	ВыводитьСообщения      = Параметры.ВыводитьСообщения;
	
	Тест_КоличествоСтрок = 0;
	Параметры.Свойство("Тест_КоличествоСтрок", Тест_КоличествоСтрок);
	
	ОтборОрганизация = 0;
	Параметры.Свойство("ОтборОрганизация", ОтборОрганизация);
	
	ОтборID = 0;
	Параметры.Свойство("ОтборID", ОтборID);
	
	Тест_НеСоздаватьСвязныеДокументы = Ложь;
	Параметры.Свойство("Тест_НеСоздаватьСвязныеДокументы", Тест_НеСоздаватьСвязныеДокументы);
	
	ТипБазы   = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(База, "ТипБД"); 
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("КонтрагентРодительФЛ",   Справочники.Контрагенты.НайтиПоНаименованию("1 Физические лица"));
	ДопПараметры.Вставить("КонтрагентРодительЮЛ",   Справочники.Контрагенты.НайтиПоНаименованию("1 Юридические лица"));
	ДопПараметры.Вставить("СвойствоID",             ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ID_Unicus"));
	ДопПараметры.Вставить("ПерезаполнятьДокументы", ПерезаполнятьДокументы);
	ДопПараметры.Вставить("ОтложенноеПроведение",   ОтложенноеПроведение);
	
	ТекстЗапроса =
	"SELECT 
	|    head.act_id as act_id,    
	|    head.НомерДок,    
	|    head.ДатаДок,    
	|    head.ДатаНачала,    
	|    head.ДатаОкончания,    
	|    head.СК,  
	|    head.СК_Договор,  
	|    tab.SK_INN as СК_ИНН,
	|    tab.SK_KPP as СК_КПП,
	|    TO_NUMBER(tab.COL2) as НомерСтроки,
	|	 tab.AGENT_INN as ОрганизацияИНН,
	|	 tab.AGENT_KPP as ОрганизацияКПП,
	|    tab.COL3 as Контрагент,
	|    tab.COL10 as КонтрагентИНН,
	|    tab.COL11 as КонтрагентКПП,
	|    REPLACE(tab.COL4 || tab.COL5, ' ', '') as НомерПолиса,
	|    TO_NUMBER(tab.COL6, '999999999999.99') as СтраховаяПремия,
	|    tab.COL7 as СуммаКВ,
	|    tab.COL8 as ПроцентКВ,
	|    tab.COL9 as СуммаКПеречислению,
  	|    tab.PAY_ID as ID
	|FROM
	|	(SELECT 
	|   		ACT_ID, 
	|		    COL2 as НомерДок, 
	|   		NVL(REGEXP_SUBSTR(COL2, '[0-9]{2}\.[0-9]{2}\.[0-9]{4}'), '01.01.0001') as  ДатаОкончания,
	|			COL3 as ДатаНачала,
	|		    COL4 as СК,
	|		    COL6 as СК_Договор,
	|			DATE_SIGN as ДатаДок
	|		FROM unicusweb_release.v_asc_act
	|		WHERE
	|			COL1 is NOT NULL
	|			AND DATE_SIGN BETWEEN &ДатаНач AND &ДатаКон
	|			AND ACT_STATUS_NAME IN ('Принят СК', 'Оплачен')
	|	) head
	|	INNER JOIN unicusweb_release.v_asc_act tab 
	|		ON tab.act_id = head.act_id
	|WHERE
	|	tab.COL1 IS NULL
	|	AND tab.PAY_DATE >= &Дата010118";
	
	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
		|	AND tab.AGENT_INN = '" + ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтборОрганизация, "ИНН") + "'";
		
		КПП = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтборОрганизация, "КПП");
		Если ЗначениеЗаполнено(КПП) Тогда
			
			ТекстЗапроса = ТекстЗапроса + "
			|	AND tab.AGENT_KPP = '" + КПП + "'";
			
		КонецЕсли;	
		
	КонецЕсли;	
		
	Если ЗначениеЗаполнено(ОтборID) Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
		|	AND head.act_id = " + Формат(ОтборID, "ЧГ=0") + "";
		
	КонецЕсли;	
	
	ТекстЗапроса = ТекстЗапроса + "
	|ORDER BY
	|	act_id, НомерСтроки";
	
	// Отладочные отборы
	//Доп отбор для тестирования
	// например AND rownum <= 10  - выбираем только первые 10 записей
	Если ЗначениеЗаполнено(Тест_КоличествоСтрок) Тогда
		
		ТекстЗапроса =  
		"SELECT
		| *
		|FROM (
		|" + ТекстЗапроса + "
		|)
		|WHERE
		|	rownum <= " + Формат(Тест_КоличествоСтрок, "ЧГ=0");
		
	КонецЕсли;	
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Дата010118", ИнтеграцияСВнешнимиСистемамиУХ.АСЦ1_асцПолучитьТекстПараметра('2018-01-01', "Oracle"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДатаНач", ИнтеграцияСВнешнимиСистемамиУХ.АСЦ1_асцПолучитьТекстПараметра(ДатаНач, "Oracle"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДатаКон", ИнтеграцияСВнешнимиСистемамиУХ.АСЦ1_асцПолучитьТекстПараметра(ДатаКон, "Oracle"));
	
	//Если ЗначениеЗаполнено(IDПоследнегоДокумента) Тогда
	//	УсловиеДата = "tab.PAY_DATE > " + Формат(IDПоследнегоДокумента, "ЧГ=0");
	//Иначе	
	//	
	//	УсловиеДата = "ДатаДок BETWEEN &ДатаНач AND &ДатаКон";
	//	
	//	УсловиеДата = СтрЗаменить(УсловиеДата, "&ДатаНач", ИнтеграцияСВнешнимиСистемамиУХ.АСЦ1_асцПолучитьТекстПараметра(ДатаНач, "Oracle"));
	//	УсловиеДата = СтрЗаменить(УсловиеДата, "&ДатаКон", ИнтеграцияСВнешнимиСистемамиУХ.АСЦ1_асцПолучитьТекстПараметра(ДатаКон, "Oracle"));
	//Конецесли;	
	//
	//ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеДата", УсловиеДата);
	
	СтрокаСоединения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(База, "СтрокаПодключения");
	
	Соединение = Новый COMОбъект("ADODB.Connection");
	Соединение.Open(СтрокаСоединения);
	
	ТаблицаДанных = АСЦ_ОбщийМодуль.ВыполнитьЗапросADO(Соединение, ТекстЗапроса);
	
	КэшДанных = Новый Структура;
	КэшДанных.Вставить("Организация",  Новый Соответствие);
	КэшДанных.Вставить("Номенклатура", Новый Соответствие);
	
	КэшСообщений = Новый Структура;
	КэшСообщений.Вставить("Организация",  Новый Соответствие);
	КэшСообщений.Вставить("Департамент",  Новый Соответствие);
	КэшСообщений.Вставить("Номенклатура", Новый Соответствие);
	
	Всего   = ТаблицаДанных.Количество();
	Счетчик = 0;
	
	ПредЗначения = Новый Структура("act_id");
	ДокОбъект    = Неопределено;
	
	Для каждого СтрокаТЗ из ТаблицаДанных Цикл
		
		Если ЗначенияОтличаются(ПредЗначения, СтрокаТЗ) Тогда
			
			ЗаполнитьЗначенияСвойств(ПредЗначения, СтрокаТЗ);
			
			Если ДокОбъект <> Неопределено Тогда
		
				Если Тест_НеСоздаватьСвязныеДокументы <> Истина Тогда
					ДокОбъект.ЗаполнитьСвязанныеДокументы();
				КонецЕсли;
		
				ДокОбъект.Проведен = Истина;
				ДокОбъект.УстановитьДатуДокумента();
				ЗаписатьДокумент(ДокОбъект, РежимЗаписиДокумента.Запись, ВыводитьСообщения, ОтложенноеПроведение);
				
			КонецЕсли;	
			
			ДокОбъект = СоздатьДокументОтчетПосредника(СтрокаТЗ, ДопПараметры, База, КэшДанных, КэшСообщений);
			
			Если Ложь Тогда
				ДокОбъект = Документы.АСЦ_ОтчетПосредника.СоздатьДокумент();
			КонецЕсли;	
			
			// Неопределено когда документ не перезаполняем
			Если ДокОбъект <> Неопределено Тогда
				
				ДокОбъект.ID_Unicus   = СтрокаТЗ.act_id;
				ДокОбъект.Дата        = СтрокаТЗ.ДатаДок;
				ДокОбъект.Организация = АСЦ_ПоискОбъектов.ПолучитьОрганизациюПоИНН(СтрокаТЗ.ОрганизацияИНН, СтрокаТЗ.ОрганизацияКПП, КэшДанных.Организация);
				
				ТекстОшибки = "";
				ДокОбъект.Контрагент  = АСЦ_ПоискОбъектов.НайтиКонтрагента("", СтрокаТЗ.СК_ИНН, СтрокаТЗ.СК_КПП, ТекстОшибки);
				Если ЗначениеЗаполнено(ДокОбъект.Контрагент) Тогда
				
					ДанныеДоговора = Новый Структура;
					ДанныеДоговора.Вставить("Контрагент",    ДокОбъект.Контрагент);
					ДанныеДоговора.Вставить("Организация",   ДокОбъект.Организация);
					ДанныеДоговора.Вставить("Наименование",  СтрокаТЗ.НомерДок);
					ДанныеДоговора.Вставить("ЦФО",           Неопределено);
					ДанныеДоговора.Вставить("ВидДоговораУХ", Перечисления.ВидыДоговоровКонтрагентовУХ.СКомитентом);
					ДанныеДоговора.Вставить("Дата",          "");
					ДанныеДоговора.Вставить("ДатаНачала",    АСЦ_ОбщийМодуль.вДату(СтрокаТЗ.ДатаНачала));
					ДанныеДоговора.Вставить("СрокДействия",  АСЦ_ОбщийМодуль.вДату(СтрокаТЗ.ДатаОкончания));
					ДанныеДоговора.Вставить("СтатьяДДС",     Справочники.СтатьиДвиженияДенежныхСредств.НайтиПоНаименованию("Выплаты по страховой деятельности"));
	
					ДокОбъект.ДоговорКонтрагента = АСЦ_ПоискОбъектов.ПолучитьДоговор(ДанныеДоговора, Ложь);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;	
		
		Счетчик = Счетчик + 1;
		ПроцентВыполнения = Окр(100 * Счетчик / Всего, 2);
		ДлительныеОперации.СообщитьПрогресс(ПроцентВыполнения, "Обработано: " + Счетчик + " из " + Всего + " строк");
		
		Если ДокОбъект <> Неопределено Тогда
			
			// Тут заполнение таб. части
			НоваяСтрока = ДокОбъект.Продажи.Добавить();
			НоваяСтрока.Документ = АСЦ_ПоискОбъектов.НайтиДокументПоСвойству(СтрокаТЗ.ID, ДопПараметры.СвойствоID, "РеализацияТоваровУслуг");
			
			Если ЗначениеЗаполнено(НоваяСтрока.Документ) Тогда
				
				РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НоваяСтрока.Документ, "Контрагент, ДоговорКонтрагента");
				ЗаполнитьЗначенияСвойств(НоваяСтрока, РеквизитыДокумента);
				
			Иначе
				
				НоваяСтрока.Контрагент = АСЦ_ПоискОбъектов.НайтиКонтрагента(СтрокаТЗ.Контрагент, СтрокаТЗ.КонтрагентИНН, СтрокаТЗ.КонтрагентКПП, ТекстОшибки);
				Если ЗначениеЗаполнено(НоваяСтрока.Контрагент) Тогда
					НоваяСтрока.ДоговорКонтрагента = АСЦ_ПоискОбъектов.НайтиДоговор(НоваяСтрока.Контрагент, ДокОбъект.Организация, СтрокаТЗ.НомерПолиса, Перечисления.ВидыДоговоровКонтрагентовУХ.СПокупателем);
				КонецЕсли;	
				
			КонецЕсли;	
			
			НоваяСтрока.СуммаОплаты  = СтрокаТЗ.СтраховаяПремия;
			НоваяСтрока.СуммаКВ      = СтрокаТЗ.СуммаКВ;
			НоваяСтрока.id           = СтрокаТЗ.ID;
			
		КонецЕсли;
		
	КонецЦикла;	
	
	Если ДокОбъект <> Неопределено Тогда
		
		Если Тест_НеСоздаватьСвязныеДокументы <> Истина Тогда
			ДокОбъект.ЗаполнитьСвязанныеДокументы();
		КонецЕсли;
		
		ДокОбъект.Проведен = Истина;
		ДокОбъект.УстановитьДатуДокумента();
		ЗаписатьДокумент(ДокОбъект, РежимЗаписиДокумента.Запись, ВыводитьСообщения, ОтложенноеПроведение);
		
	КонецЕсли;	
		
КонецПроцедуры	

Функция СоздатьДокументОтчетПосредника(Данные, Параметры, База, КэшДанных, КэшСообщений)
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("ИД", Данные.act_id); 
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.АСЦ_ОтчетПосредника КАК Док
	|ГДЕ
	|	Док.ID_Unicus = &ИД";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		
		ДокументСсылка = Результат.Выгрузить()[0][0];
		
		Если Параметры.ПерезаполнятьДокументы = Истина Тогда
			ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
			ДокументОбъект.Продажи.Очистить();
			ДокументОбъект.Проведен = Ложь;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
	Иначе
		ДокументОбъект = Документы.АСЦ_ОтчетПосредника.СоздатьДокумент();
	КонецЕсли;
	
	Возврат ДокументОбъект;
	
КонецФункции	

#КонецОбласти

