
&НаКлиенте
Процедура Прочитать(Команда)
	ПрочитатьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНаСервере()
	
	СтрокаСоединения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(База, "СтрокаПодключения");
	
	Соединение = Новый COMОбъект("ADODB.Connection");
	Соединение.Open(СтрокаСоединения);
	
	ТекстЗапроса =
	"SELECT TOP 20
	|	[CarOrder_ID]
	|      ,[CarOrderNumber]
	|      ,CAST(CarSale_ID as VARCHAR(36)) as CarSale_ID
	|      ,[CarSaleNumber]
	|      ,[Project_ID]
	|      ,[ProjectName]
	|      ,CAST([Center_ID] as VARCHAR(36)) as Center_ID
	|      ,[CenterName]
	|      ,[Department_ID]
	|      ,[DepartmentName]
	|      ,CAST([Firm_ID] as VARCHAR(36)) as Firm_ID
	|      ,[FirmName]
	|      ,[CFR_ID]
	|      ,[CFRName]
	|      ,[Cars_ID]
	|      ,[VIN]
	|      ,[CarMarkName]
	|      ,[CarModelName]
	|      ,[Client_ID]
	|      ,[ClientName]
	|      ,[Bank_ID]
	|      ,[BankName]
	|      ,[BankINN]
	|      ,[BankKPP]
	|      ,[CreditSumma]
	|      ,[FullCreditSumma]
	|      ,[CreditDealDate]
	|      ,[SaleDate]
	|      ,[CreditPayDate]
	|FROM 
	|	[Cash_Flow_Porsche_Test].[uho].[VW_CarSale]
	|WHERE
	|	SaleDate BETWEEN &Дата1 AND &Дата2
	|	AND FullCreditSumma > 0";
	
	Если ЗначениеЗаполнено(ДЦ) Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	AND Center_ID = '" + ДЦ.УникальныйИдентификатор() + "'";
	КонецЕсли;	
	
	ТекстЗапроса = ТекстЗапроса + "
	|ORDER BY
	|	SaleDate";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Дата1", ИнтеграцияСВнешнимиСистемамиУХ.АСЦ1_асцПолучитьТекстПараметра(Период.ДатаНачала, "SQLOLEDB"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Дата2", ИнтеграцияСВнешнимиСистемамиУХ.АСЦ1_асцПолучитьТекстПараметра(Период.ДатаОкончания, "SQLOLEDB"));
	
	ТЗ = АСЦ_ОбщийМодуль.ВыполнитьЗапросADO(Соединение, ТекстЗапроса);
	
	Таблица.Очистить();
	Для каждого СтрокаТЗ из ТЗ цикл
		
		НоваяСтрока = Таблица.Добавить();
		НоваяСтрока.ГУИД        = СтрокаТЗ.CarSale_ID;
		НоваяСтрока.Номер       = СтрокаТЗ.CarSaleNumber;
		НоваяСтрока.Организация = Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор(СтрокаТЗ.Firm_ID));
		НоваяСтрока.ДЦ          = Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор(СтрокаТЗ.Center_ID));
		НоваяСтрока.Дата        = СтрокаТЗ.SaleDate;
		НоваяСтрока.VIN         = СтрокаТЗ.VIN;
		
		Если ЗначениеЗаполнено(СтрокаТЗ.CreditPayDate) Тогда
			НоваяСтрока.ДатаКВ = СтрокаТЗ.CreditPayDate;
			
		ИначеЕсли ЗначениеЗаполнено(СтрокаТЗ.CreditDealDate) Тогда	
			НоваяСтрока.ДатаКВ = СтрокаТЗ.CreditDealDate;
			
		Иначе	
			НоваяСтрока.ДатаКВ = СтрокаТЗ.SaleDate;
		КонецЕсли;	
		
		НоваяСтрока.СуммаКредита = СтрокаТЗ.FullCreditSumma;
		НоваяСтрока.Банк = АСЦ_ПоискОбъектов.НайтиКонтрагента(СтрокаТЗ.BankName, СтрокаТЗ.BankINN, СтрокаТЗ.BankKPP);
		НоваяСтрока.Покупатель = АСЦ_ПоискОбъектов.НайтиКонтрагента(СтрокаТЗ.ClientName,"","");
	
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокумент(Команда)
	СоздатьДокументНаСервере();
КонецПроцедуры

&НаСервере
Процедура СоздатьДокументНаСервере()
	
	ТекущаяСтрока = Элементы.Таблица.ТекущаяСтрока;
	СтрокаТЗ = Таблица.НайтиПоИдентификатору(ТекущаяСтрока);
	
	ГУИД = Новый УникальныйИдентификатор(СтрокаТЗ.ГУИД);
	ДокументСсылка = Документы.РеализацияТоваровУслуг.ПолучитьСсылку(ГУИД);
	Если ОбщегоНазначения.СсылкаСуществует(ДокументСсылка) Тогда
		ДокОбъект = ДокументСсылка.ПолучитьОбъект();
	Иначе	
		ДокОбъект = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
		ДокОбъект.УстановитьСсылкуНового(ДокументСсылка);
	КонецЕсли;	
	
	ДокОбъект.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Товары;
	ДокОбъект.ВалютаДокумента = Константы.ВалютаРегламентированногоУчета.Получить();
	ДокОбъект.Организация = СтрокаТЗ.Организация;
	ДокОбъект.Дата = СтрокаТЗ.Дата;
	ДокОбъект.Номер = Прав(СтрокаТЗ.Номер, 11);
	ДокОбъект.Контрагент = СтрокаТЗ.Покупатель;
	ДокОбъект.Записать();
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Строка(ДокОбъект.Ссылка);
	Сообщение.КлючДанных = ДокументСсылка;
	Сообщение.Сообщить();
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Реализация", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.АСЦ_ПлановоеНачислениеКВ КАК Док
	|ГДЕ
	|	Док.Реализация = &Реализация";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		ДокКВ = Результат.Выгрузить()[0][0].ПолучитьОбъект();
	Иначе	
		ДокКВ = Документы.АСЦ_ПлановоеНачислениеКВ.СоздатьДокумент();
		ДокКВ.Реализация = ДокументСсылка;
	КонецЕсли;
	
	ДокКВ.ВидОперации = Перечисления.АСЦ_ВидыОперацийПлановоеНачислениеКВ.КВБанков;
	ДокКВ.ДЦ    = СтрокаТЗ.ДЦ;
	ДокКВ.VIN   = СтрокаТЗ.VIN;
	ДокКВ.Дата  = СтрокаТЗ.ДатаКВ;
	ДокКВ.Сумма = СтрокаТЗ.СуммаКредита;
	ДокКВ.Комитент = СтрокаТЗ.Банк;
	ДокКВ.Контрагент = СтрокаТЗ.Покупатель;
	ДокКВ.Статья = Справочники.СтатьиДоходовИРасходов.НайтиПоНаименованию("КВ банков новые а/м");
	ДокКВ.Записать();
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Строка(ДокКВ.Ссылка);
	Сообщение.КлючДанных = ДокКВ.Ссылка;
	Сообщение.Сообщить();
	
КонецПроцедуры
