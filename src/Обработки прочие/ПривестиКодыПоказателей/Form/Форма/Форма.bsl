
&НаКлиенте
Процедура Прочитать(Команда)
	ПрочитатьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНаСервере()
	
	ТаблицаПоказателей.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("ВидОтчета", ВидОтчета);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПоказателиОтчетов.Ссылка КАК Ссылка,
	|	ПоказателиОтчетов.Код КАК Код,
	|	ПоказателиОтчетов.Колонка.Код КАК КолонкаКод,
	|	ПоказателиОтчетов.Строка.Код КАК СтрокаКод
	|ИЗ
	|	Справочник.ПоказателиОтчетов КАК ПоказателиОтчетов
	|ГДЕ
	|	НЕ ПоказателиОтчетов.ПометкаУдаления
	|	И ПоказателиОтчетов.Владелец В ИЕРАРХИИ (&ВидОтчета)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Код = СокрЛП(Выборка.СтрокаКод) + "_" + СокрЛП(Выборка.КолонкаКод);
		Если Код = СокрЛП(Выборка.Код) Тогда
			Продолжить;
		КонецЕсли;	
		
		НоваяСтрока = ТаблицаПоказателей.Добавить();
		НоваяСтрока.Показатель = Выборка.Ссылка;
		НоваяСтрока.Код        = Код;
		
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьКоды(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИзменитьКодыЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, "Изменить коды показателей?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьКодыЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьКодыНаСервере();
	Состояние("Коды изменены");
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьКодыНаСервере()
	
	Для каждого СтрокаТЗ из ТаблицаПоказателей Цикл
		
		СпрОбъект = СтрокаТЗ.Показатель.ПолучитьОбъект();
		СпрОбъект.Код = СтрокаТЗ.Код;
		СпрОбъект.Записать();
		
	КонецЦикла;	
	
КонецПроцедуры
