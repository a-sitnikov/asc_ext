
&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	ТекстЗапроса = 
	"SELECT
	|	pay_id
	|FROM
	|	unicusweb.v_asc_pay_contract 
	|WHERE
	|	PAY_DATE >= &ДатаНач
	|	AND PAY_DATE <= &ДатаКон
	|	AND pay_id IS NOT NULL";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДатаНач", ИнтеграцияСВнешнимиСистемамиУХ.АСЦ1_асцПолучитьТекстПараметра(Период.ДатаНачала, "Oracle"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДатаКон", ИнтеграцияСВнешнимиСистемамиУХ.АСЦ1_асцПолучитьТекстПараметра(Период.ДатаОкончания, "Oracle"));
	
	СтрокаСоединения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(База, "СтрокаПодключения");
	
	Соединение = Новый COMОбъект("ADODB.Connection");
	Соединение.Open(СтрокаСоединения);
	
	ТЗ = АСЦ_ОбщийМодуль.ВыполнитьЗапросADO(Соединение, ТекстЗапроса);
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("ТЗ", ТЗ);
	Запрос.Параметры.Вставить("Имя", "ID_UNICUS");
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таб.pay_id КАК pay_id
	|ПОМЕСТИТЬ ДанныеUNICUS
	|ИЗ
	|	&ТЗ КАК Таб
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДополнительныеСведения.Объект КАК Документ,
	|	ВЫРАЗИТЬ(ДополнительныеСведения.Значение КАК ЧИСЛО) КАК ID
	|ИЗ
	|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеUNICUS КАК ДанныеUNICUS
	|		ПО ((ВЫРАЗИТЬ(ДополнительныеСведения.Значение КАК ЧИСЛО)) = ДанныеUNICUS.pay_id)
	|ГДЕ
	|	ДополнительныеСведения.Объект ССЫЛКА Документ.РеализацияТоваровУслуг
	|	И ДополнительныеСведения.Свойство.Имя = &Имя
	|	И ДанныеUNICUS.pay_id ЕСТЬ NULL
	|	И ВЫРАЗИТЬ(ДополнительныеСведения.Значение КАК ЧИСЛО) <> 0";
	
	ТЗ2 = Запрос.Выполнить().Выгрузить();
	Таблица.Загрузить(ТЗ2);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Таблица.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ПоказатьЗначение(, ТекущиеДанные.Документ);
	
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УдалитьОтвет", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, "Удалить документы?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьОтвет(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьСервер();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСервер()
	
	Для каждого СтрокаТЗ из Таблица Цикл
		
		ДокументОбъект = СтрокаТЗ.Документ.ПолучитьОбъект();
		ДокументОбъект.Удалить();
		
	КонецЦикла;	
	
КонецПроцедуры